% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Vengeance is Hex

constants:

   include blakston.khd

resources:

   vengeance_cast_rsc = "Mystical vengeant spines surround %s%s."
   vengeance_already_enchanted = \
      "That poor soul is already afflicted by vengeance."
   vengeance_start = "Mystical vengeant spines climb along your limbs and glimmer menacingly!"
   vengeance_stop  = "The mystical spines fade."
   has_been_hexed_vengeance = \
      "Mystical vengeant spines surround %s%s!"

   vengeance_hurt_you = \
      "The spines around your limbs flare, reflecting ~B~r%i~n damage!"

   vengeance_name_rsc = "vengeance"
   vengeance_icon_rsc = ihexbecloud.bgf
   vengeance_desc_rsc = \
      "Surrounds your victim with mystical spines that inflict vengeance upon them "
      "in proportion to elemental damage that they deal. "
      "This hex requires one shock fragment to cast."

   vengeance_sound = qenfeebl.wav

   vengeance_hex_effect_report = \
      "At that effectiveness, your vengeance hex will reflect ~r~B%i\% ~nof the victim's elemental damage back at them." 

   vengeance_death_msg = \
      "~B~U~k[###]~n ~B~v%s was killed by reflected elemental damage!"

classvars:

   vrName = vengeance_name_rsc
   vrIcon = vengeance_icon_rsc
   vrDesc = vengeance_desc_rsc

   vrHexEffectReport = vengeance_hex_effect_report

   vrAlreadyEnchanted = vengeance_already_enchanted
   vrEnchantment_On = vengeance_start
   vrEnchantment_Off = vengeance_stop
   vrSuccess = vengeance_cast_rsc
   vrInformRoom = has_been_hexed_vengeance

   viSpell_num = SID_HEX_VENGEANCE
   viSchool = SS_WITCHERY
   viSubSchool = SSS_WITCHERY_VENTDAL
   viSpell_level = 4
   viMana = 12
   viSpell_Exertion = 22

   viFlash = FLASH_BAD

   vrSucceed_wav = vengeance_sound
   viChance_To_Increase = 15
   viMeditate_ratio = 70

properties:

   % Reflect this percentage of elemental damage base
   piBaseEffect = 25

   piFlatDuration = 60000

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&ShockFragment,1],plReagents);

      return;
   }

   ReflectElementalDamage(who=$,what=$,damage=0,state=0,atype=0,aspell=0,stroke_obj=$)
   {
      local iPercentDamage, iReflectedDamage, iFinalDamage;

      if who = $
         OR what = $
         OR who = what
         OR damage <= 0
         OR state < 0
      {
         return;
      }
      
      if IsClass(what,&Player)
      {
         if NOT Send(what,@IsLoggedOn)
         {
            return;
         }
         if Send(what,@IsInCannotInteractMode)
         {
            return;
         }
         if Send(what,@GetOwner) <> $
            AND IsClass(Send(what,@GetOwner),&Room)
            AND NOT Send(Send(what,@GetOwner),@ReqSomethingAttack,#what=what,#victim=who,#report=FALSE)
         {
            return;
         }
      }

      % stroke_obj is the monster object if the monster is attacking
      % who is the victim
      % what is the attacker
      % the attacker is the one that's cursed with Thorns
      iPercentDamage = piBaseEffect + (piBaseEffect * (state+1))/100;
      iReflectedDamage = (damage * iPercentDamage)/100;

      iFinalDamage = 0;
      if aspell <> 0
      {
         iFinalDamage = Send(what,@AssessDamage,#what=self,#damage=iReflectedDamage,
            #atype=0,
            #aspell=aspell,
            #precision=TRUE,
            #absolute=TRUE,
            #damaging_now=TRUE);
         
         if IsClass(what,&Player)
            AND iFinalDamage <> $
         {
            Post(what,@MsgSendUser,#message_rsc=vengeance_hurt_you,#parm1=iFinalDamage/100);
         }
      }

      if iFinalDamage = $
      {
         Send(what,@Killed);
         if IsClass(what,&Player)
         {
            Send(self,@SendDeathMessage,#who=what);
         }
      }

      return;
   }
   
   SendDeathMessage(who=$)
   {
      local i;

      foreach i in Send(SYS,@GetUsersLoggedOn)
      {
         Send(i,@MsgSendUser,#message_rsc=vengeance_death_msg,
               #parm1=Send(who,@GetName));
      }

      return;
   }

   DoHexEffectReport(what=$,iHexEffect=0)
   {
      Send(what,@MsgSendUser,#message_rsc=vrHexEffectReport,#parm1=(piBaseEffect + (piBaseEffect * (iHexEffect+1))/100));
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
