% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Hold is Debuff

constants:

   include blakston.khd

resources:

   include hold.lkod

   hold_name_rsc = "hold"
   hold_icon_rsc = ihold.bgf
   hold_desc_rsc = \
      "Surrounds the target with magical energy, causing "
      "paralysis for several seconds.  "
      "Requires two purple mushrooms to cast."

   hold_already_enchanted = "%s%s is already held."
   hold_caster = "%s%s is now held in place by a magical force."
   hold_on = "A magical tingling pulses through your body.  You are unable to move."
   hold_off = "The magical hold lifts and you are able to move once more."
   
   hold_break_rod_active = \
      "Flowing energies dissipate your spell before you even begin!"

   hold_player_rsc = \
      holdrings.bgf

   hold_ring_projectile_rsc = \
      blindring.bgf

classvars:

   vrName = hold_name_rsc
   vrIcon = hold_icon_rsc
   vrDesc = hold_desc_rsc

   vrAlreadyEnchanted = hold_already_enchanted
   vrEnchantment_On = hold_on
   vrEnchantment_Off = hold_off
   vrSuccess = hold_caster

   viSpell_num = SID_HOLD
   viSchool = SS_QOR
   viSpell_level = 4

   viSpellExertion = 2
   viMana = 15

   viChance_To_Increase = 10
   viMeditate_ratio = 50

   viFlash = FLASH_BAD

   % In seconds, since it works off GetTime()
   viPostCast_time = 2 % Recommended value 1.

   vrHoldRings = hold_player_rsc

   viLayer = 9

   vrProjectile_Icon = hold_ring_projectile_rsc

   vbIsRangedAttack = FALSE

   viProjectileFlag = PROJ_FLAG_LIGHT_SOURCE

   % Projectile lighting information.
   viProjectileLightFlags = LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC
   % Medium light range, out of 255.
   viProjectileLightIntensity = 100
   % Default color is lightning.
   viProjectileLightColor = LIGHT_BPURPLE
   
   viProjectileSpeed = 5

   viPrimaryStat = STAT_ID_MYSTICISM
   viSecondaryStat = STAT_ID_KARMA

properties:

   piBaseDuration = 2000 % Recommended value 2000.
   piSpellPowerModifier = 40 % Recommended value 30.
   piDeviation = 50 % Recommended value 10%.
   viCast_time = 0 % Recommended value 600 (milliseconds).

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&PurpleMushroom,2],plReagents);

      return;
   }

   CanPayCosts(who=$,lTargets=$,bItemCast=FALSE)
   {
      local target, i;

      % Can cast spell if the 1 target item is a user
      if Length(lTargets) <> 1
      {
         return FALSE;
      }

      target = First(lTargets);

      % TODO: this is not an ideal implementation of blind/hold resistance.
      % These should be enchantments themselves, rather than requiring
      % multiple checks of every item in the target's inventory.
      if IsClass(target,&Player)
      {
         foreach i in Send(target,@GetHolderPassive)
         {
            if IsClass(i,&BreakRodHold)
               AND Send(i,@IsActive)
            {
               Send(who,@MsgSendUser,#message_rsc=hold_break_rod_active);

               return FALSE;
            }
         }
      }

      if IsClass(who,&Player)
      {
         foreach i in Send(who,@GetHolderPassive)
         {
            if IsClass(i,&BreakRodHold)
               AND Send(i,@IsSecondaryActive)
            {
               Send(who,@MsgSendUser,#message_rsc=hold_break_rod_active);

               return FALSE;
            }
         }
      }

      propagate;
   }

   CastSpell(who=$,wielder=$,lTargets=$,iSpellpower=0,report=TRUE,oDeflectImpact=$,bDeflectCast=FALSE)
   {
      local oTarget, oRoom, i, iDeflectSpeed;

      oTarget = First(lTargets);
      % Is this a proc from a weapon?
      if wielder = $
      {
         oRoom = Send(who,@GetOwner);
         if oDeflectImpact <> $
            AND oRoom <> $
         {
            % Don't send to oDeflectImpact, it's not a NoMoveOn!
            iDeflectSpeed = Send(who,@SquaredFineDistanceTo,#what=oDeflectImpact);
               
            if iDeflectSpeed = 0
            {
               iDeflectSpeed = $;
            }

            if iDeflectSpeed <> $
            {
               iDeflectSpeed = (Sqrt(iDeflectSpeed) / 64) * 2;
            }
            Send(oRoom,@SomethingShot,#who=who,#target=oDeflectImpact,#projectile=self,#flags=viProjectileFlag,#iDeflectSpeed=iDeflectSpeed);
         }
         else
         {
            if oRoom <> $
            {
               Send(oRoom,@SomethingShot,#who=who,#target=oTarget,#projectile=self,#flags=viProjectileFlag);
            }
         }
      }
      else
      {
         oRoom = Send(wielder,@GetOwner);
         if oDeflectImpact <> $
            AND oRoom <> $
         {
            % Don't send to oDeflectImpact, it's not a NoMoveOn!
            iDeflectSpeed = Send(wielder,@SquaredFineDistanceTo,#what=oDeflectImpact);
               
            if iDeflectSpeed = 0
            {
               iDeflectSpeed = $;
            }

            if iDeflectSpeed <> $
            {
               iDeflectSpeed = (Sqrt(iDeflectSpeed) / 64) * 2;
            }
            Send(oRoom,@SomethingShot,#who=wielder,#target=oDeflectImpact,#projectile=self,#flags=viProjectileFlag,#iDeflectSpeed=iDeflectSpeed);
         }
         else
         {
            if oRoom <> $
            {
               Send(oRoom,@SomethingShot,#who=wielder,#target=oTarget,#projectile=self,#flags=viProjectileFlag);
            }
         }
      }

      % Spell effects.
      Send(self,@DoSpell,#what=who,#oTarget=oTarget,#report=report,
            #iDuration=Send(self,@GetDuration,#iSpellPower=iSpellPower));

      propagate;
   }

   DoSpell(what=$,oTarget=$,iDuration=$,report=TRUE,bAllowFreeAction=TRUE)
   "Holds target for duration seconds."
   {
      local i, oSpell;

      if oTarget = $
      {
         return FALSE;
      }

      % Check for enchantment already applied
      if Send(oTarget,@IsEnchanted,#what=self)
      {
         return FALSE;
      }

      % Check for spell called with $ iDuration, apply default time if so.
      if iDuration = $
      {
         iDuration = 5000;
      }

      % Check if the target has free action, unless not allowed.
      if bAllowFreeAction
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_FREE_ACTION);
         if Send(oTarget,@IsEnchanted,#what=oSpell)
         {
            iDuration = Send(oSpell,@DoFreeAction,#oHoldCaster=what,
                              #oTarget=oTarget,#iDuration=iDuration);
            if iDuration = $
            {
               % Resisted completely
               return FALSE;
            }
      
            if Send(oTarget,@GetHoldResistance) <> 0
            {
               iDuration = (iDuration * (100-Send(oTarget,@GetHoldResistance)))/100;
            }

            if IsClass(oTarget,&Player)
            {
               iDuration = Bound(iDuration,500,10000);
            }
            else
            {
               iDuration = Bound(iDuration,50,$);
            }
         }
      }

      Send(oTarget,@StartEnchantment,#what=self,#time=iDuration,#report=report,
            #state=report);

      if IsClass(oTarget,&Player)
      {
         if report
         {
            Send(oTarget,@MsgSendUser,#message_rsc=vrEnchantment_On);
         }

         % Do not paralyze DMs.
         if NOT (IsClass(oTarget,&DM) AND Send(oTarget,@PlayerIsImmortal))
         {
            Send(oTarget,@EffectSendUser,#what=self,#effect=EFFECT_PARALYZE_ON);
         }
      }
      else
      {
         Send(oTarget,@ResetBehaviorFlags);
         if Send(oTarget,@GetOwner) <> $
         {
            Send(Send(oTarget,@GetOwner),@SomethingChanged,#what=oTarget);
         }
         Post(oTarget,@WasHeld);
      }

      if report
      {
         Send(what,@MsgSendUser,#message_rsc=vrSuccess,
               #parm1=Send(oTarget,@GetCapDef),#parm2=Send(oTarget,@GetName));
      }

      propagate;
   }

   GetDuration(iSpellPower=0)
   {
      local iDuration;

      % All duration calculations now use class properties, modifiable in game
      % By default, base duration 2000ms, total time 2000-6000ms using 40x
      % spellpower multiplier, with the final duration 50-100% max
      iDuration = piBaseDuration + iSpellPower * piSpellPowerModifier;
      iDuration = Random(iDuration * (100 - piDeviation) / 100,iDuration);

      return iDuration;
   }

   EndEnchantment(who=$,report=TRUE,state=$)
   {
      if who <> $
         AND IsClass(who,&Player)
      {
         Send(who,@RemoveOverlay,#what=self);
      }
      
      if IsClass(who,&Player)
      {
         % Don't override phasing hold.
         if NOT Send(who,@CheckPlayerFlag,#flag=PFLAG_PHASED)
         {
            Send(who,@EffectSendUser,#what=self,#effect=EFFECT_PARALYZE_OFF);
         }

         Send(who,@EffectSendUser,#what=self,#effect=EFFECT_PARALYZE_OFF);
         if state
         {
            Send(who,@MsgSendUser,#message_rsc=vrEnchantment_Off);
         }
      }
      else  % class is &Monster
      {
         % This has to be done AFTER the enchantment is gone from the
         % monster's ench list - Post it
         Post(who,@ResetBehaviorFlags);
         if Send(who,@GetOwner) <> $
         {
            Post(Send(who,@GetOwner),@SomethingChanged,#what=who);
         }
      }

      return;
   }

   RestartEnchantmentEffect(who=$,state=$)
   {
      Send(who,@MsgSendUser,#message_rsc=vrEnchantment_On);
      Send(who,@EffectSendUser,#what=self,#effect=EFFECT_PARALYZE_ON);

      return;
   }

   ModifyMonsterBehavior(mob=$)
   {
      Send(mob,@SetBehaviorFlag,#flag=AI_NOMOVE,#value=TRUE);
      Send(mob,@SetBehaviorFlag,#flag=AI_NOFIGHT,#value=TRUE);

      return;
   }

   SetSpellPlayerFlag(who=$)
   {
      if NOT (IsClass(who,&DM) AND Send(who,@PlayerIsImmortal))
      {
         Send(who,@SetPlayerFlag,#flag=PFLAG_NO_MOVE,#value=TRUE);
         Send(who,@SetPlayerFlag,#flag=PFLAG_NO_FIGHT,#value=TRUE);
         Send(who,@SetPlayerFlag,#flag=PFLAG_NO_MAGIC,#value=TRUE);
      }

      return;
   }

   GetLayer()
   {
      return viLayer;
   }
   
   GetOverlayHotspot()
   {
      return HS_LEGS;
   }

   SendOverlayAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE, 4,80, 2,1, 2,5);
    %  AddPacket(1,ANIMATE_NONE,2,1); % group 2 is the player-overlay group
      
      return;
   }

   SendInventoryOverlays()
   {
      AddPacket(1, 0);
      
      return;
   }

   SendOverlayInformation()
   {
      AddPacket(4,Send(self,@GetOverlay));
      AddPacket(1,Send(self,@GetOverlayHotspot));
      Send(self,@SendOverlayAnimation);
      
      return;
   }

   GetNumberOverlays()
   {
      % Most overlay items only offer one overlay.
      %  Some items like weapons and bows may offer two.
      return 1;
   }

   GetOverlay(animation = $)
   {
      return hold_player_rsc;
   }

   % Return the icon used to display a spell shooting through the air.
   GetProjectileIcon()
   {
      return vrProjectile_Icon;
   }

   % Return the speed at which a particular spell should appear to shoot through the air.
   GetProjectileSpeed(who=$, target=$)
   {
      local iDist;

      if who <> $
         AND target <> $
      {
         % only NoMoveOns can calculate distances
         if IsClass(who,&NoMoveOn)
         {
            iDist = Send(who,@SquaredFineDistanceTo,#what=target);
         }
         else if IsClass(target,&NoMoveOn)
         {
            iDist = Send(target,@SquaredFineDistanceTo,#what=who);
         }
         else
         {
            % Neither is a NoMoveOn? Give up!
            return viProjectileSpeed;
         }

         if iDist <> $
            AND iDist <> 0
         {
            iDist = Sqrt(iDist);

            % Check sqrt value for 0
            if iDist = 0
            {
               return viProjectileSpeed;
            }
            else
            {
               return Bound((iDist / 64) * 2,viProjectileSpeed,$);
            }
         }
      }
      return viProjectileSpeed;
   }

   SendProjectileAnimation()
   {
      AddPacket(1, ANIMATE_CYCLE, 4, 40, 2, 1, 2, 6);   % 40ms between animations
      return;
   }

   GetProjectileLightFlags()
   {
      return viProjectileLightFlags;
   }

   GetProjectileLightIntensity()
   {
      return viProjectileLightIntensity;
   }

   GetProjectileLightColor()
   {
      return viProjectileLightColor;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
