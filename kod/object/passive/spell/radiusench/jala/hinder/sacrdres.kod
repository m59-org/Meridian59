% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
SacredResonance is HinderSpell

constants:

   include blakston.khd

resources:

   include sacrdres.lkod

   SacredResonance_name_rsc = "sacred resonance"
   SacredResonance_icon_rsc = isacrres.bgf
   SacredResonance_desc_rsc = \
      "The music of this song hinders the casting of the dark spells of Qor "
      "and protects all who hear it from blinding and dazzling magics. "
      "In addition, listeners are protected from Qor's preferred elements, "
      "unholy and acid. "
      "A ruby and an entroot berry are required to begin performing this song."
      
   sacred_resist_rsc = \
      "\n\nIn addition to hindering the magics of Qor, the song of Sacred Resonance currently affecting "
      "you adds %i\% unholy resistance and acid resistance."

   SacredResonance_song = goodfor.mp3

classvars:

   vrName = SacredResonance_name_rsc
   vrIcon = SacredResonance_icon_rsc
   vrDesc = SacredResonance_desc_rsc

   viMana = 5          % Mana is amount used upon inititiation
   viManaDrain = 3     % Drain is amount used every viDrainTime milliseconds
   viDrainTime = 5000    % Drain some mana every viDrainTime milliseconds
   viSpell_num = SID_SACRED_RESONANCE

   viSchool = SS_JALA
   viSpell_level = 3

   viHinderedSchool = SS_QOR

   viResistOne = -ATCK_SPELL_UNHOLY
   viResistTwo = -ATCK_SPELL_ACID

   viDivisorOne = 6
   viDivisorTwo = 6

properties:

   prMusic = SacredResonance_song

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Ruby,1],plReagents);
      plReagents = Cons([&EntrootBerry,1],plReagents);

      return;
   }

   AddBlindResistance(power=0,resistance=0)
   {
      return resistance + power/3;
   }

   SendStatsDesc(who=$)
   {
      if who <> $
         AND IsClass(who,&User)
         AND Send(who,@GetMostPowerfulRadiusEnchantmentState,#byClass=&SacredResonance) <> $
      {
         Send(self,@EffectDesc,#who=who);

         return;
      }

      propagate;
   }

   EffectDesc(who=$)
   {
      local lState, iDivisorOne;
      
      lState = Send(who,@GetMostPowerfulRadiusEnchantmentState,#byClass=&SacredResonance);

      if lState <> $
      {
         iDivisorOne = viDivisorOne;
         if iDivisorOne = 0
         {
            iDivisorOne = 1;
         }
         AddPacket(4,sacred_resist_rsc, 4,Nth(lState,2)/iDivisorOne);
      }

      propagate;
   }

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
