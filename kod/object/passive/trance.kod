% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Trance is PassiveObject 

constants:

   include blakston.khd

resources:

   include trance.lkod

   trance_begin = "You focus your whole will on casting %s."
   trance_break = "Your concentration is broken and the %s spell fizzles."
   trance_break_sound = spelfail.wav

classvars:

properties:

messages:

   GetSpellNum()
   {
      return SID_TRANCE;
   }

   BeginCastingTrance( what = $, who = $, lTargets = $, time = 0 )
   {
      local oWarpTime;

      oWarpTime = Send(SYS,@FindSpellByNum,#num=SID_WARPTIME);
      if Send(Send(who,@GetOwner),@IsEnchanted,#what=oWarpTime)
      {
         %time = Send(oWarpTime,@GetStretchedTime,#oldtime=time,
         %            #oRoom=Send(who,@GetOwner));
      }
      
      Send(who,@StartEnchantment,#what=self,#time=time,#addicon=FALSE,
           #state=[what,who,lTargets,TRUE]);

      return;
   }

   SetSpellPlayerFlag(who=$)
   {
      Send(who,@SetTranceFlag);

      return;
   }

   EndEnchantment(who = $, state = $ )
   "Called when:"
   " 1) Casting time expires, time to call CastSpell"
   " 2) Spell expires"
   {
      local oSpell, oCaster, lTargets, iSpellPower, oDeflectImpact;
      
      oSpell = Nth(state,1);
      oCaster = Nth(state,2);
      lTargets = Nth(state,3);
      Send(oCaster,@ClearTranceFlag);

      % Double-check to get valid targets.  Don't report 'cause we've already 
      %  spammed them during the first check.
      lTargets = Send(oSpell,@GetGoodTargets,#who=oCaster,#lTargets=lTargets,
                      #report=FALSE);

      iSpellPower = Send(oSpell,@GetSpellPower,#who=oCaster);
      if Send(oSpell,@DoubleCheckAfterTrance,#who=oCaster,#lTargets=lTargets,
              #iSpellPower=iSpellPower)
      {

         oDeflectImpact = $;
         if lTargets <> $
            AND IsClass(First(lTargets),&Battler)
         {
            oDeflectImpact = Send(First(lTargets),@TryDeflect,#what=oSpell,#caster=oCaster);
         }
         
         if oDeflectImpact <> $
         {
            Send(oSpell,@CastSpell,#who=oCaster,#lTargets=[oCaster],#iSpellPower=iSpellPower/2,#oDeflectImpact=oDeflectImpact,#bDeflectCast=TRUE);
         }             
         else
         {
            Send(oSpell,@CastSpell,#who=oCaster,#lTargets=lTargets,
                 #iSpellpower=iSpellpower);
         }
      }
      
      return;
   }

   BreakTrance(who = $, state = $, event = EVENT_CAST,time_remaining=0)
   {
      local oSpell, oCaster;

      if state = $
         AND who <> $
      {
         state = Send(who,@GetRawState,#what=self);
      }

      oSpell = First(state);
      oCaster = Nth(state,2);
      
      if event <> EVENT_STEER
      {
         if Send(oCaster,@GetSkillAbility,#Skill_num=SKID_VOLATILE_FOCUS) > 0
         {
            if Send(Send(SYS,@FindSkillByNum,#num=SKID_VOLATILE_FOCUS),
                        @AttemptTimeSacrifice,
                        #who=oCaster,
                        #oSpell=oSpell,
                        #iSpellPower=Send(oSpell,@GetSpellPower,#who=oCaster),
                        #time_remaining=time_remaining,
                        #event=event)
            {
               % We successfully avoided a disruption.
               return;
            }
         }

         Send(oCaster,@ClearTranceFlag);
         Send(oSpell,@TranceBroken,#oCaster=oCaster,#state=state);
      }
      else
      {
         Send(oCaster,@ClearTranceFlag);
      }
      
      Send(oCaster,@CastOrFizzledNoteBloodMagic);
      
      Send(oCaster,@RemoveEnchantment,#what=self);
      
      return;
   }

   RemoveEnchantmentEffects( who = $, state = $ )
   "This is normally translated to EndEnchantment in spell.kod"
   "For trance, it is just swallowed."
   {
      return;
   }

   ShowEnchantmentIcon() 
   {
      return False;
   }

   IsPersonalEnchantment()
   {
      return FALSE;
   }

   AffectsMaxMana()
   {
      return FALSE;
   }

   ModifyResistance(resistance_list=$)
   {
      return resistance_list;
   }

   % Just here because sometimes trance acts like a spell
   UseHexBehavior()
   {
      return FALSE;
   }
   
   ModifyEmpowerment(empowerment_list=$)
   {
      return empowerment_list;
   }

   AltersLightLevel()
   {
      return FALSE;
   }
   
   ModifySlayer(slayer_list=$, iState=0)
   {
      return slayer_list;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
