% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Cleave is Stance

   % This stance modifies basic attacks.
   % A Weaponcraft 5 player in this stance will attempt to deal melee damage
   %    to multiple nearby enemies when attacking their target.
   %
   % Cleave works with fencing weapons, riija swords, axes, and scimitars.
   % Regardless of the weapon used, the damage type becomes slashing.

constants:

   include blakston.khd
   include protocol.khd   

resources:

   Cleave_name_rsc = "cleave"
   Cleave_icon_rsc = ikillfel.bgf
   Cleave_desc_rsc = \
      "By shifting his or her feet and striking from a longer angle, "
      "a master soldier can employ a heavy bladed melee weapon against multiple enemies rather than just one. "
      "Such attacks may do less than full damage, but will still be deadly. Cleaving can be done with "
      "fencing weapons, axes, and scimitars, but the maneuver will result in a slashing attack type "
      "regardless of weapon employed. A cleaving attack will attempt to strike all enemies within "
      "range. Axes are the most effective weapon for cleaving."

   Cleave_skill_intro = \
      "Weaponcraft Lv. 5: A stance that allows the warrior to swing at multiple enemies."
   
   Cleave_begin = "You shift your feet and raise your arms."
   Cleave_end = "You lower your arms and return to a regular combat stance."
   Cleave_mantra = "Fau Srauv"
   
   cleave_attack_rsc = "You swing your weapon in a cleaving arc!"

classvars:

   vrName = Cleave_name_rsc
   vrIcon = Cleave_icon_rsc
   vrDesc = Cleave_desc_rsc
   
   stance_begin_msg = Cleave_begin
   stance_end_msg = Cleave_end
   
   vsStanceMantra = Cleave_mantra

   vrSkill_intro = Cleave_skill_intro

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_CLEAVE
   viSchool = SKS_FENCING
   viSkill_Level = 5
   viChance_to_Increase = 5
   viMeditate_ratio = 90

   viSkillExertion = 0
   vbCheck_exertion = FALSE

   vbAutomatic = TRUE
   
   viCleaveExertion = 1
   
   viPrimaryStat = STAT_ID_MIGHT
   viSecondaryStat = 0

properties:

messages:

   GetRequisiteStat(who=$)
   {
      return Send(who,@GetMight);
   }

   GetCleaveAttackType(weapon_used=$)
   {
      local iAttackType;

      if weapon_used <> $
      {
         iAttackType = Send(weapon_used,@GetAttackType);
         
         if (iAttackType & ATCK_WEAP_THRUST)
         {
            iAttackType = iAttackType & ~ATCK_WEAP_THRUST;
         }
         
         if (iAttackType & ATCK_WEAP_BLUDGEON)
         {
            iAttackType = iAttackType & ~ATCK_WEAP_BLUDGEON;
         }
         
         iAttackType = iAttackType | ATCK_WEAP_SLASH;
         return iAttackType;
      }

      return ATCK_WEAP_SLASH + ATCK_WEAP_NONMAGIC;
   }

   GetCleaveDamage(who=$, damage=0)
   {
      local iCleaveAbility, iStat, iDamagePercentage, oWeapon, iWeaponModifier;
      
      oWeapon = Send(who,@GetWeapon);
      iCleaveAbility = Send(who,@GetSkillAbility,#skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);

      % Cleaving attacks do as little as 40% damage,
      % and as much as 80% damage at 70 Might and 99%.
      
      iWeaponModifier = 100;
      if oWeapon <> $
      {
         % Different types of weapons are better or worse for cleaving
         iWeaponModifier = Send(oWeapon,@GetCleaveModifier);
      }

      iDamagePercentage = ((40 + iCleaveAbility/3 + iStat/10) * (iWeaponModifier))/100;
      iDamagePercentage = Bound(iDamagePercentage,1,83);

      if Send(who,@GetMasteredSkill) = SKID_PROFICIENCY_AXE
         AND Send(who,@IsUsingA,#class=&Axe)
      {
         % Axe masters exert much less.
         % This is still going to be a pretty significant drain, considering it's per monster.
         Send(who,@AddExertion,#amount=500*viCleaveExertion);
      }
      else
      {
         Send(who,@AddExertion,#amount=10000*viCleaveExertion);
      }
      Send(self,@ImproveAbility,#who=who);

      return (damage * iDamagePercentage)/100;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
