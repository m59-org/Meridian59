% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
FocusedMind is Stance

constants:

   include blakston.khd
   include protocol.khd   

resources:

   FocusedMind_name_rsc = "focused mind"
   FocusedMind_icon_rsc = irestor.bgf
   FocusedMind_desc_rsc = "A master stance that aids regeneration of health at the cost of spiritual rejuvenation."
   
   FocusedMind_begin = "The world sharpens and comes into focus as you let mana flow through you."
   FocusedMind_end = "You relax your thoughts."
   FocusedMind_mantra = "Havyrik Pemk"

classvars:

   vrName = FocusedMind_name_rsc
   vrIcon = FocusedMind_icon_rsc
   vrDesc = FocusedMind_desc_rsc
   
   stance_begin_msg = FocusedMind_begin
   stance_end_msg = FocusedMind_end
   
   vsStanceMantra = FocusedMind_mantra

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_FOCUSED_MIND
   viSchool = SKS_FENCING
   viSkill_Level = 6
   viChance_to_Increase = 2
   viMeditate_ratio = 50

   viSkillExertion = 0
   vbCheck_exertion = FALSE

   vbAutomatic = TRUE
   
   viPrimaryStat = STAT_ID_INTELLECT
   viSecondaryStat = 0

properties:

   piAbilityDivisor = 3
   piStatDivisor = 5
   piRejuvenationTimeMultiple = 4

messages:

   GetRequisiteStat(who=$)
   {
      return Send(who,@GetIntellect);
   }

   ModifyRegenerationTime(who=$,iTime=60000)
   {
      local iFocusedMindAbility, iStat, iTimeReduction;

      iFocusedMindAbility = Send(who,@GetSkillAbility,#skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);
      
      if piAbilityDivisor = 0
      {
         piAbilityDivisor = 1;
      }
      if piStatDivisor = 0
      {
         piStatDivisor = 1;
      }

      % 3% base, up to 33% from skill and 14% from stat, total 50%
      iTimeReduction = 3 + (iFocusedMindAbility / piAbilityDivisor) + (iStat / piStatDivisor);
      iTimeReduction = bound(iTimeReduction,3,50);

      if NOT Send(who,@IsResting)
      {
         Send(self,@ImproveAbility,#who=who);
      }
      
      return iTime * (100-iTimeReduction) / 100;
   }

   ModifyRejuvenationTime(who=$,iTime=60000)
   {
      local iFocusedMindAbility, iStat, iTimeIncrease;

      iFocusedMindAbility = Send(who,@GetSkillAbility,#skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);

      % Up to four times as slow mana time
      iTimeIncrease = ((iFocusedMindAbility+1)*piRejuvenationTimeMultiple);
      iTimeIncrease = bound(iTimeIncrease,100,$);

      return iTime * iTimeIncrease / 100;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
