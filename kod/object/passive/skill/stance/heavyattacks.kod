% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
HeavyAttackStance is Stance

constants:

   include blakston.khd
   include protocol.khd   

resources:

   HeavyAttacks_name_rsc = "forceful impacts"
   HeavyAttacks_icon_rsc = istreng.bgf
   HeavyAttacks_desc_rsc = \
      "Knights are masters of slow, calculated, and heavy combat. "
      "While in this stance, warriors will perform attacks more slowly, "
      "but when they do swing, the result will be tremendous."
      "\n\nHeavy attacks are only possible with weaponcraft "
      "melee weapons. Other tools, like daggers and bows, "
      "are not designed for honorable combat."

   HeavyAttacks_skill_intro = "Knightcraft Lv. 1: A stance that slows attack speed but increases power."
   
   HeavyAttacks_begin = "You raise your shoulders and prepare to strike with all your might."
   HeavyAttacks_end = "You return your upper body to its normal position."
   HeavyAttacks_mantra = "For my liege!"

   HeavyAttacks_intro = \
      "Knightcraft Lv. 3: Slower attacks, more power."

classvars:

   vrName = HeavyAttacks_name_rsc
   vrIcon = HeavyAttacks_icon_rsc
   vrDesc = HeavyAttacks_desc_rsc

   stance_begin_msg = HeavyAttacks_begin
   stance_end_msg = HeavyAttacks_end
   
   vsStanceMantra = HeavyAttacks_mantra

   vrSkill_intro = HeavyAttacks_skill_intro

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_HEAVY_ATTACK_STANCE
   viSchool = SKS_KNIGHTCRAFT
   viSkill_Level = 3
   viChance_to_Increase = 5
   viMeditate_ratio = 40

   viSkillExertion = 0
   vbCheck_exertion = FALSE

   vbAutomatic = TRUE
   
   viHeavyAttackExertion = 1
   
   viPrimaryStat = STAT_ID_MIGHT
   viSecondaryStat = 0

properties:

messages:

   GetRequisiteStat(who=$)
   {
      return Send(who,@GetMight);
   }

   ModifyAttackTime(who=$, iAttackTime=0, stroke_obj = $)
   {
      if stroke_obj <> $
         AND IsClass(stroke_obj,&Slash)
      {
         return iAttackTime*3;
      }
      return iAttackTime;
   }

   ModifyAttackDamage(who=$, what=$, damage=0, stroke_obj=$)
   {
      local iHeavyAttackAbility, iStat, iDamagePercentage;

      if stroke_obj = $
         OR NOT IsClass(stroke_obj,&Slash)
      {
         return damage;
      }

      iHeavyAttackAbility = Send(who,@GetSkillAbility,#skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);

      % Heavy attacks do as little as 200% damage,
      % and as much as 300% damage at 100 Might and 99%.
      % This skill only takes a quarter of its effectiveness from Might,
      % since Might is already part of weapon damage.

      iDamagePercentage = 200 + (((iHeavyAttackAbility+1)*3)/4) + (iStat/4);
      iDamagePercentage = Bound(iDamagePercentage,200,300);

      Send(who,@AddExertion,#amount=10000*viHeavyAttackExertion);
      Send(self,@ImproveAbility,#who=who);

      return (damage * iDamagePercentage)/100;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
