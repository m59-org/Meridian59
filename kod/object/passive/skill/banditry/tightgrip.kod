% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
TightGrip is BanditrySkill

constants:

   include blakston.khd
   include protocol.khd   

resources:

   TightGrip_name_rsc = "tight grip"
   TightGrip_icon_rsc = parryico.bgf
   TightGrip_desc_rsc = "Allows a trained fighter to resist disarm attempts."
   
   TightGrip_success = "%s attempts to disarm you, but you grip your weapon tightly and divert the force of the strike!"
   TightGrip_resisted = "%s resists your disarm attempt!"

classvars:

   vrName = TightGrip_name_rsc
   vrIcon = TightGrip_icon_rsc
   vrDesc = TightGrip_desc_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_TIGHT_GRIP
   viSchool = SKS_BANDITRY
   viSkill_Level = 5
   viChance_to_Increase = 40
   viMeditate_ratio = 200

   viSkillExertion = 0
   vbCheck_exertion = FALSE

   vbAutomatic = TRUE

properties:

messages:

   GetRequisiteStat(who=$)
   {
      return Send(who,@GetMight);
   }

   DoSkill(who=$,what=$,oWeapon=$)
   {
      local chance, iAgility, iAbility;
      
      iAgility = Send(self,@GetRequisiteStat,#who=who);
      iAbility = Send(who,@GetSkillAbility,#skill_num=viSkill_num);
      
      chance = (iAgility/2) + iAbility;

      % Harder to resist punch and dagger disarms
      if oWeapon = $
         OR IsClass(oWeapon,&Dagger)
      {
         chance = chance / 2;
      }
      
      chance = bound(chance,5,95);
      
      if chance > Random(0,100)
      {
         Send(who,@MsgSendUser,#message_rsc=TightGrip_success,#parm1=Send(what,@GetName));
         
         if IsClass(what,&Player)
         {
            Send(what,@MsgSendUser,#message_rsc=TightGrip_resisted,#parm1=Send(who,@GetName));
         }

         Send(self,@ImproveAbility,#who=who,#target=what);  

         return TRUE;
      }

      return FALSE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
