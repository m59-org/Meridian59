% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Weaponmaster is KnightcraftSkill

constants:

   include blakston.khd
   include protocol.khd   

resources:

   Weaponmaster_name_rsc = "weapon mastery"
   Weaponmaster_icon_rsc = fenceico.bgf
   Weaponmaster_desc_rsc = \
      "Career knights endure many campaigns, not all of which "
      "are well supplied. Sometimes, combat comes down to "
      "fighting with whatever makeshift weapons are on hand, "
      "so grizzled veterans come to feel at home with any instrument "
      "of violence. "
      "In extreme cases, veteran knights have even been known to "
      "resort to touch spells."
      "\n\nWeapon masters have improved chance to hit with "
      "all weapons and touch spells. The smarter the knight, the better the acquired career skills."

classvars:

   vrName = Weaponmaster_name_rsc
   vrIcon = Weaponmaster_icon_rsc
   vrDesc = Weaponmaster_desc_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_WEAPONMASTER
   viSchool = SKS_KNIGHTCRAFT
   viSkill_Level = 6
   viChance_to_Increase = 4
   viMeditate_ratio = 110

   viSkillExertion = 0
   vbCheck_exertion = FALSE

   vbAutomatic = TRUE
   
   viPrimaryStat = STAT_ID_INTELLECT
   viSecondaryStat = 0

properties:

messages:

   GetRequisiteStat(who=$)
   {
      return Send(who,@GetIntellect);
   }

   GetWeaponmasterPercentage(who=$,what=$)
   {
      local iStat, iAbility;
      
      if IsClass(who,&Monster)
      {
         % No monster weaponmasters... for now
         return 0;
      }
      
      iStat = Send(self,@GetRequisiteStat,#who=who);
      iAbility = Send(who,@GetSkillAbility,#skill_num=viSkill_num);
         
      if iAbility = 0
      {
         % Player does not have the skill!
         return 0;
      }

      if Send(who,@GetWeapon) = $
      {
         % Does not help punch, but touch spells aren't $
         return 0;
      }

      Send(self,@ImproveAbility,#who=who);  

      % up to 100 from ability and up to another 100 from Intellect
      % This adds directly to to hit, meaning 20% chance to land an attack
      return iAbility + 1 + iStat;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
