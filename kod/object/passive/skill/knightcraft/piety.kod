% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Piety is KnightcraftSkill

constants:

   include blakston.khd
   include protocol.khd   

resources:

   Piety_name_rsc = "piety"
   Piety_icon_rsc = iboonagi.bgf
   Piety_desc_rsc = \
      "Chief among the virtues of a knight, beyond all qualities of character, "
      "is that of moral standing. Above fighters, brawlers, mercenaries, "
      "soldiers, and guards, a knight stands as a symbol of righteousness. "
      "Those knights who have pledged themselves fully to the cause "
      "bear some essence in kind with crusaders."
      "\n\nBy aligning one's spirit with the energies of holiness "
      "and crusading, piety provides resistance to holy purging effects."

   Piety_skill_intro = \
      "Knightcraft Lv. 5: Purge resistance."

   pious_resistance = \
      "~B%s resists your purging energies by sheer force of piety!"

classvars:

   vrName = Piety_name_rsc
   vrIcon = Piety_icon_rsc
   vrDesc = Piety_desc_rsc

   vrSkill_intro = Piety_skill_intro

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_PIETY
   viSchool = SKS_KNIGHTCRAFT
   viSkill_Level = 5
   viChance_to_Increase = 5
   viMeditate_ratio = 75

   viSkillExertion = 0
   vbCheck_exertion = FALSE

   vbAutomatic = TRUE
   
   viPrimaryStat = STAT_ID_MYSTICISM
   viSecondaryStat = 0

properties:

messages:

   GetRequisiteStat(who=$)
   {
      return Send(who,@GetMysticism);
   }

   GetPurgeResistance(who=$,caster=$)
   {
      local iSkillAbility, iStat, iMonsterBonus;

      iSkillAbility = Send(who,@GetSkillAbility,#skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);

      Send(self,@ImproveAbility,#who=who);

      if caster <> $
         AND IsClass(caster,&Monster)
      {
         iMonsterBonus = 2;
      }
      else
      {
         iMonsterBonus = 1;
      }
      
      if caster <> $
         AND IsClass(caster,&Player)
      {
         Send(caster,@MsgSendUser,#message_rsc=pious_resistance,#parm1=Send(who,@Getname));
      }

      % Up to 33% resistance from skill and 20% from Myst (if 100 myst somehow)
      % Double it if the purger is a monster
      return (iSkillAbility/3 + iStat/5)*iMonsterBonus;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
