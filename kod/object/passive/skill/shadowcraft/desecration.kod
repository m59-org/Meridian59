% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Desecration is ShadowcraftSkill

constants:

   include blakston.khd

resources:

   Desecration_name_rsc = "desecration"
   Desecration_icon_rsc = idefile.bgf
   Desecration_desc_rsc = \
      "The life of a dedicated servant of balance comes with a small ability to tip the scales. "
      "By filtering outgoing destructive energies with the inner strength of one's "
      "spirit, a master monk can change the very nature of a mystical attack. "
      "If successful, holy attacks and spells hitting enemies will become unholy instead, "
      "while shock attacks and spells will become acid, and fire attacks and spells will become cold."
   Desecration_mastered_desc_rsc = \
      "\n\n"
      "You possess exceptional mastery of this skill."

classvars:

   vrName = Desecration_name_rsc
   vrIcon = Desecration_icon_rsc
   vrDesc = Desecration_desc_rsc
   vrMasteredDesc = Desecration_mastered_desc_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_DESECRATION
   viSchool = SKS_SHADOWCRAFT
   viSkill_Level = 4
   viChance_to_Increase = 12
   viMeditate_ratio = 40

   viSkillExertion = 0
   vbCheck_exertion = FALSE
   
   viPrimaryStat = STAT_ID_MYSTICISM
   viSecondaryStat = 0

properties:

messages:

   GetRequisiteStat(who=$)
   {
      return Send(who,@GetMysticism);
   }

   ConvertDamageTypes(who=$,aspell=0)
   {
      local iAbility, iStat, bFound;

      iAbility = Send(who,@GetSkillAbility,#Skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);

      Send(self,@ImproveAbility,#who=who);

      if iAbility + iStat/10 > Random(1,100)
      {
         bFound = FALSE;
         if aspell & ATCK_SPELL_HOLY
         {
            aspell = aspell & ~ATCK_SPELL_HOLY;
            aspell = aspell | ATCK_SPELL_UNHOLY;
            bFound = TRUE;
         }
         if aspell & ATCK_SPELL_SHOCK
         {
            aspell = aspell & ~ATCK_SPELL_SHOCK;
            aspell = aspell | ATCK_SPELL_ACID;
            bFound = TRUE;
         }
         if aspell & ATCK_SPELL_FIRE
         {
            aspell = aspell & ~ATCK_SPELL_FIRE;
            aspell = aspell | ATCK_SPELL_COLD;
            bFound = TRUE;
         }
         
         if bFound
         {
            Send(self,@DoSkill,#who=who);
         }
      }
      return aspell;
   }

   DoSkill(who=$)
   {
      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
