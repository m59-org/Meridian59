% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BloodMagic is SorcerySkill

constants:

   include blakston.khd

resources:

   blood_magic_name_rsc = "blood magic"
   blood_magic_icon_rsc = imajheal.bgf
   blood_magic_desc_rsc = \
      "By the sacrifice of some of one's own life essence, a mage can replace "
      "the need for physical reagents."
      
   not_enough_health = \
      "You don't have enough health to cast this spell!"
   sacrifice_health = \
      "You spill your own blood in place of physical reagents, "
      "losing %i health."

classvars:

   vrName = blood_magic_name_rsc
   vrIcon = blood_magic_icon_rsc
   vrDesc = blood_magic_desc_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_num = SKID_BLOOD_MAGIC
   viSchool = SKS_SORCERY
   viSkill_Level = 2
   viChance_to_Increase = 30
   viMeditate_ratio = 100

   viSkillExertion = 0
   vbCheck_exertion = FALSE

properties:

messages:

   GetRequisiteStat(who=$)
   {
      return Send(who,@GetStamina);
   }
   
   AttemptBloodSacrifice(who=$,oSpell=$,iSpellPower=0)
   {
      local iAbility, iStat, iSpellAbility, iSacrifice, iCoefficient;

      iAbility = Send(who,@GetSkillAbility,#Skill_num=viSkill_num);
      iSpellAbility = Send(who,@GetSpellAbility,
                               #spell_num=Send(oSpell,@GetSpellNum));
      iStat = Send(self,@GetRequisiteStat,#who=who);
      
      iSacrifice = Send(oSpell,@GetManaCost,#who=who,#iSpellPower=iSpellPower);
      iCoefficient = Bound(298 - iAbility - iSpellAbility,100,300);
      
      iSacrifice = (iSacrifice * iCoefficient)/100;
      
      if iSacrifice >= Send(who,@GetHealth)
      {
         Send(who,@MsgSendUser,#message_rsc=not_enough_health);
         return FALSE;
      }
      
      Send(self,@DoSkill,#who=who,#iSacrifice=iSacrifice);
      
      return TRUE;
   }

   DoSkill(who=$,iSacrifice=0)
   {
      Send(who,@LoseHealth,#amount=iSacrifice);
      Send(who,@MsgSendUser,#message_rsc=sacrifice_health,
                            #parm1=iSacrifice);
      
      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
