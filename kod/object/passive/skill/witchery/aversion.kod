% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Aversion is WitcherySkill

constants:

   include blakston.khd

resources:

   aversion_name_rsc = "aversion"
   aversion_icon_rsc = iseance.bgf
   aversion_desc_rsc = \
      "Witches and warlocks have a lengthy history of using familiars to do "
      "battle with one another. Ventdal has made a pact with entities "
      "unknown to mankind to protect her servants from attack by any mindless form. "
      "If that pact is successfully drawn upon, minions of any type and most undead enemies "
      "will deal less damage to the witch being attacked."

   aversion_protect = \
      "Strange entities reach out and protect you from the minion's attack because of your pact with Ventdal!"

classvars:

   vrName = aversion_name_rsc
   vrIcon = aversion_icon_rsc
   vrDesc = aversion_desc_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_Num = SKID_AVERSION
   viSubSchool = SSS_WITCHERY_VENTDAL
   viSkill_level = 5
   viChance_to_increase = 10
   viMeditate_ratio = 60
   
   viPrimaryStat = STAT_ID_MYSTICISM
   viSecondaryStat = 0

properties:
   
   piMinionDamageReduction = 20

messages:

   GetRequisiteStat(who=$)
   "Varies from skill to skill and spell to spell."
   {
      return Send(who,@GetMysticism);
   }

   AvertDamage(who=$,oAttacker=$,damage=0)
   {
      local iAbility, iStat, iPercentageDecrease;
      
      if oAttacker = $
         OR NOT IsClass(oAttacker,&Monster)
         OR (NOT Send(oAttacker,@IsMinion)
             AND NOT Send(oAttacker,@IsUndead))
      {
         return FALSE;
      }
      
      iAbility = Send(who,@GetSkillAbility,#Skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);
      
      iPercentageDecrease = piMinionDamageReduction;
      
      iPercentageDecrease = iPercentageDecrease + iStat/5 + ((iPercentageDecrease * (iAbility+1))/100);

      return (damage * (100-iPercentageDecrease))/100;
   }

   DoSkill(who=$,oAttacker=$)
   {
      local iAbility, iStat;
      
      if oAttacker = $
         OR NOT IsClass(oAttacker,&Monster)
         OR (NOT Send(oAttacker,@IsMinion)
             AND NOT Send(oAttacker,@IsUndead))
      {
         return FALSE;
      }
      
      iAbility = Send(who,@GetSkillAbility,#Skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);

      if bound(iAbility-6,1,93) + iStat/10 <= Random(1,100)
      {
         return FALSE;
      }

      Send(who,@MsgSendUser,#message_rsc=aversion_protect);

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
