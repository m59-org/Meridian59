% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
AncientPact is WitcherySkill

constants:

   include blakston.khd

resources:

   ancient_pact_name_rsc = "ancient pact"
   ancient_pact_icon_rsc = slashico.bgf
   ancient_pact_desc_rsc = \
      "Alliances made long ago with minor demons allow students of Iwodach "
      "to increase the durations of their hexes."

   ancient_pact_increase = \
      "You call upon an ancient pact to increase the duration of your hex."

classvars:

   vrName = ancient_pact_name_rsc
   vrIcon = ancient_pact_icon_rsc
   vrDesc = ancient_pact_desc_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_Num = SKID_ANCIENT_PACT
   viSubSchool = SSS_WITCHERY_IWODACH
   viSkill_level = 4
   viChance_to_increase = 16
   viMeditate_ratio = 45

properties:

   plPrerequisites = $

   piChanceToIncrease = 100
   
   piDurationIncrease = 15

messages:

   GetRequisiteStat(who=$)
   "Varies from skill to skill and spell to spell."
   {
      return Send(who,@GetMysticism);
   }

   GetDurationIncrease(who=$)
   {
      local iAbility, iStat, iTotalIncrease;
      
      iAbility = Send(who,@GetSkillAbility,#Skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);
      
      iTotalIncrease = piDurationIncrease;
      
      iTotalIncrease = iTotalIncrease + iStat/10 + 
                        ((iTotalIncrease * (iAbility+1))/100);

      return iTotalIncrease;
   }

   DoSkill(who=$)
   {
      local iAbility, iStat;
      
      iAbility = Send(who,@GetSkillAbility,#Skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);

      if bound(iAbility-6,1,93) + iStat/10 <= Random(1,100)
      {
         return FALSE;
      }

      Send(who,@MsgSendUser,#message_rsc=ancient_pact_increase);

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
