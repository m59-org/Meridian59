% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ArcaneKnowledge is WitcherySkill

constants:

   include blakston.khd

resources:

   arcane_knowledge_name_rsc = "arcane knowledge"
   arcane_knowledge_icon_rsc = slashico.bgf
   arcane_knowledge_desc_rsc = \
      "Studying the ancient demonic arts allows a clever practitioner "
      "to resist the effects of hexes."

   arcane_knowledge_resist = \
      "You resist a portion of the hex."

classvars:

   vrName = arcane_knowledge_name_rsc
   vrIcon = arcane_knowledge_icon_rsc
   vrDesc = arcane_knowledge_desc_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viSkill_Num = SKID_ARCANE_KNOWLEDGE
   viSubSchool = SSS_WITCHERY_XAERDUN
   viSkill_level = 2
   viChance_to_increase = 18
   viMeditate_ratio = 35

properties:

   plPrerequisites = $

   piChanceToIncrease = 100
   
   piHexResistance = 15

messages:

   GetRequisiteStat(who=$)
   "Varies from skill to skill and spell to spell."
   {
      return Send(who,@GetMysticism);
   }

   GetHexResistance(who=$)
   {
      local iAbility, iStat, iTotalResistance;
      
      iAbility = Send(who,@GetSkillAbility,#Skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);
      
      iTotalResistance = piHexResistance;
      
      iTotalResistance = iTotalResistance + iStat/10 + 
                        ((iTotalResistance * (iAbility+1))/100);

      return iTotalResistance;
   }

   DoSkill(who=$)
   {
      local iAbility, iStat;
      
      iAbility = Send(who,@GetSkillAbility,#Skill_num=viSkill_num);
      iStat = Send(self,@GetRequisiteStat,#who=who);

      if bound(iAbility-6,1,93) + iStat/10 <= Random(1,100)
      {
         return FALSE;
      }

      Send(who,@MsgSendUser,#message_rsc=arcane_knowledge_resist);

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
