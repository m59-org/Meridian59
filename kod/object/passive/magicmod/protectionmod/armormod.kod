% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ArmorMod is ProtectionMod

constants:

   include blakston.khd

resources:

   armor_mod_name = "armor"

classvars:

   viModName = armor_mod_name
   viModRangeMin = 1
   viModRangeMax = 6
   
   viPowerDivisor = 50
   viResistanceType = ATCK_WEAP_ALL
   viModGroup = GROUP_ARMOR

properties:

   piNumerator = 1
   piDivisor = 3

messages:

   ModifyDefenseDamage(who=$,what=$,damage=$,atype=0,aspell=0,iState=0)
   {
      local iDamageReduce;

      iDamageReduce = 0;
      if Send(SETTINGS_OBJECT,@GetDirectArmorEnabled)
      {
         if iState <> 0
         {
            iDamageReduce = random(iState*33,iState*100);
            iDamageReduce = bound(iDamageReduce,0,(damage-100));
         }

         if aspell <> 0
         {
            % Reduce damage if part of the damage is spell
            if atype <> 0
            {
               % Only 2/3 of the damage reduction if we're doing both weapon
               % and spell damage types.
               iDamageReduce = iDamageReduce * 2 / 3;
            }
            else
            {
               % If we're doing pure spell damage, we get no reduction.
               iDamageReduce = 0;
            }
         }
      }
      
      return damage - iDamageReduce;
   }

   ModifyResistance(resistance_list=$,iState=0)
   {
      if piDivisor = 0
      {
         % Avoid div errors
         piDivisor = 1;
      }

      resistance_list = Send(SYS,@AddResistance,#what=viResistanceType,#value=bound((iState*piNumerator)/piDivisor,1,$),#resistance_list=resistance_list);
      return resistance_list;
   }

   GetNumber(iPower=0)
   {
      if piDivisor = 0
      {
         piDivisor = 1;
      }
      return Bound((iPower*piNumerator)/piDivisor,1,$);
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
