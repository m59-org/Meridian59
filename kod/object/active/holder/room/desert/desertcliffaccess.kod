% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DesertCliffAccess is DesertRoom

constants:

   include blakston.khd

resources:

   include desertcliffaccess.lkod

   room_desertcliffaccess = desertcliffaccess.roo
   room_name_desertcliffaccess = "Sandbreak Cliffs"

classvars:

   vrName = room_name_desertcliffaccess

   viTeleport_row = 50
   viTeleport_col = 40

   vbSpawnMonsters = TRUE
   viMaxSpawnCol = 66

properties:

   piBaseLight = LIGHT_DARK
   piOutside_factor = OUTDOORS_5 

   piDirectional_percent = DIRECTIONAL_PERCENT_OUTDOORS

   prRoom = room_desertcliffaccess
   piRoom_num = RID_DESERTCLIFFACCESS

messages:

   SomethingMoved(what = $,new_row = $, new_col = $)
   {
      local oTargetRoom;
      if new_col < 2
      {
         oTargetRoom = Send(Send(SYS,@GetDesertRoomMaintenance),@GetDesertDunes3);
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=oTargetRoom,
              #new_row=Send(oTargetRoom,@GetTeleportRow,#direction=ANGLE_WEST),
              #new_col=Send(oTargetRoom,@GetTeleportCol,#direction=ANGLE_WEST),
              #new_angle=Send(what,@GetAngle));
         return;
      }

      if new_col > (Send(self,@GetRoomCols)-2)
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTPATH1),
              #new_row=10,
              #new_col=10,
              #new_angle=Send(what,@GetAngle));
         return;
      }

      if new_row < 2
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTCLIFFNOACCESS1),
              #new_row=Send(self,@GetRoomRows)-6,
              #new_col=40,
              #fine_row=0,
              #fine_col=0,
              #new_angle=Send(what,@GetAngle));
         return;
      }

      if new_row > (Send(self,@GetRoomRows)-2)
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTCLIFFNOACCESS3),
              #new_row=6,
              #new_col=40,
              #fine_row=0,
              #fine_col=0,
              #new_angle=Send(what,@GetAngle));
         return;
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
