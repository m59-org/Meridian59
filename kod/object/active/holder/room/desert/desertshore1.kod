% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DesertShore1 is DesertRoom

constants:

   include blakston.khd

resources:

   include desertshore1.lkod

   room_desertshore1 = desertshore1.roo
   room_name_desertshore1 = "Solitary Shoreline"

   no_travel_into_sea = "There's nothing but death for you out there."

classvars:

   vrName = room_name_desertshore1

   viTeleport_row = 60
   viTeleport_col = 102

   vbSpawnMonsters = TRUE
   viMinSpawnCol = 49

properties:

   piBaseLight = LIGHT_DARK
   piOutside_factor = OUTDOORS_5 

   piDirectional_percent = DIRECTIONAL_PERCENT_OUTDOORS

   prRoom = room_desertshore1
   piRoom_num = RID_DESERTSHORE1

messages:

   SomethingMoved(what = $,new_row = $, new_col = $)
   {
      local oTargetRoom;
      if new_col > (Send(self,@GetRoomCols)-2)
      {
         oTargetRoom = Send(Send(SYS,@GetDesertRoomMaintenance),@GetDesertDunes1);
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=oTargetRoom,
              #new_row=Send(oTargetRoom,@GetTeleportRow,#direction=ANGLE_EAST),
              #new_col=Send(oTargetRoom,@GetTeleportCol,#direction=ANGLE_EAST),
              #new_angle=Send(what,@GetAngle));
         return;
      }

      if new_col < 2
      {
         if IsClass(what,&User)
         {
            Send(what,@MsgSendUser,#message_rsc=no_travel_into_sea);
         }
         return;
      }

      if new_row < 2
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTSHORE2),
              #new_row=Send(self,@GetRoomRows)-6,
              #new_col=60,
              #fine_row=0,
              #fine_col=Send(what,@GetFineCol),
              #new_angle=Send(what,@GetAngle));
         return;
      }

      if new_row > (Send(self,@GetRoomRows)-2)
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTSHORE3),
              #new_row=6,
              #new_col=60,
              #fine_row=0,
              #fine_col=Send(what,@GetFineCol),
              #new_angle=Send(what,@GetAngle));
         return;
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
