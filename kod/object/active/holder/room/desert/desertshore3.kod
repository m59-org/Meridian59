% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DesertShore3 is DesertRoom

constants:

   include blakston.khd

resources:

   include desertshore3.lkod

   room_desertshore3 = desertshore3.roo
   room_name_desertshore3 = "Solitary Shoreline"

   no_travel_into_sea3 = \
      "The underwater terrain drops off sharply here, preventing progress."
   travel_into_sea3 = \
      "You set out onto the oceanic ice."

classvars:

   vrName = room_name_desertshore3

   viTeleport_row = 60
   viTeleport_col = 102

properties:

   piBaseLight = LIGHT_DARK
   piOutside_factor = OUTDOORS_5 

   piDirectional_percent = DIRECTIONAL_PERCENT_OUTDOORS

   prRoom = room_desertshore3
   piRoom_num = RID_DESERTSHORE3

messages:

   SomethingMoved(what = $,new_row = $, new_col = $)
   {
      local oTargetRoom;

      if new_col > (Send(self,@GetRoomCols)-2)
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTDUNES),
              #new_row=Send(what,@GetRow),
              #new_col=3,
              #fine_row=Send(what,@GetFineRow),
              #fine_col=0,
              #new_angle=Send(what,@GetAngle));
         return;
      }

      if new_col < 2
      {
         if IsClass(what,&User)
         {
            if Send(self,@CheckRoomFlag,#flag=ROOM_SNOWING)
            {
               oTargetRoom = Send(SYS,@FindRoomByNum,#num=RID_DESERTSANDBAR);
               Send(what,@MsgSendUser,#message_rsc=travel_into_sea3);
               Send(SYS,@UtilGoNearSquare,#what=what,
                    #where=oTargetRoom,
                    #new_row=Send(what,@GetRow),
                    #new_col=Send(oTargetRoom,@GetRoomCols)-3,
                    #fine_row=Send(what,@GetFineRow),
                    #fine_col=0,
                    #new_angle=Send(what,@GetAngle));
            }
            else
            {
               Send(what,@MsgSendUser,#message_rsc=no_travel_into_sea3);
            }
         }
         return;
      }

      if new_row < 2
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTSHORE1),
              #new_row=Send(self,@GetRoomRows)-3,
              #new_col=Send(what,@GetCol),
              #fine_row=0,
              #fine_col=Send(what,@GetFineCol),
              #new_angle=Send(what,@GetAngle));
         return;
      }

      if new_row > (Send(self,@GetRoomRows)-2)
      {
         Send(SYS,@UtilGoNearSquare,#what=what,
              #where=Send(SYS,@FindRoomByNum,#num=RID_DESERTSHORE2),
              #new_row=3,
              #new_col=Send(what,@GetCol),
              #fine_row=0,
              #fine_col=Send(what,@GetFineCol),
              #new_angle=Send(what,@GetAngle));
         return;
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

