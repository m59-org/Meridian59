% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
JasperTomb is GuildHall

% Custom hall built for a guild called Haunted on 103.
% 
% Tomb's portals are unique in that their destinations depend on the items in the reliquaries next to them.
% Items only need to be briefly put inside, although someone will have to stay behind to remove them after portal use.

constants:

   AREA_OUTSIDE = 0
   AREA_INSIDE = 1

   ENTRANCE_DOOR = 100
   ENTRANCE_CLOSE_TIME = 18000

   SECRET_DOOR = 101
   SECRET_CLOSE_TIME = 5000
   
   SPECTRAL_MOVE_TIME = 1000
   
   MISC_FOYER_SECTIONS = 102
   INSIDE_SECTIONS = 103

   include blakston.khd

resources:

   room_name_jaspertomb = "Haunted Tomb"
   room_jaspertomb = jaspertomb.roo

   jaspertomb_sound = stoneup2.wav

   news_jaspertomb_name = "news"
   news_jaspertomb_desc = "This is the guild newsgroup."
   
   jasper_tomb_portal_desc = \
      "You see a somber window into a memory of someone long departed. "
      "This portal will take you to a place reminiscent of the item you put in the reliquary to honor them."

   jasper_tomb_music = cave2.mp3

classvars:

   vrName = room_name_jaspertomb

   viLongitude = 3000
   viLatitude = 3001

   viNews_row = 18
   viNews_col = 27
   viNews_finerow = 0
   viNews_finecol = 0

   viTeleport_row = 4
   viTeleport_col = 16

   viFoyer_north = 1
   viFoyer_south = 13
   viFoyer_east = 32
   viFoyer_west = 5

   viLever_row = 8
   viLever_col = 11
   viLever_fine_row = 32
   viLever_fine_col = 32

   viInner_teleport_row = 15
   viInner_teleport_col = 13

   viQuality = 12
   viTerrain_type = TERRAIN_CITY | TERRAIN_GUILDHALL | TERRAIN_CAVES
   viSeconds_til_close = 18
   
   viPortalOneRow = 2
   viPortalOneCol = 29
   viPortalOneFineRow = 0
   viPortalOneFineCol = 0
   viPortalOneAngle = ANGLE_SOUTH
   viPortalOneDestRID = 0
   viPortalOneDestRow = 42
   viPortalOneDestCol = 48
   viPortalOneDestFineRow = 8
   viPortalOneDestFineCol = 37
   viPortalOneDestAngle = ANGLE_NORTH_WEST
   vsPortalOneDesc = jasper_tomb_portal_desc
   
   viPortalTwoRow = 22
   viPortalTwoCol = 7
   viPortalTwoFineRow = 0
   viPortalTwoFineCol = 0
   viPortalTwoAngle = ANGLE_SOUTH
   viPortalTwoDestRID = 0
   viPortalTwoDestRow = 30
   viPortalTwoDestCol = 20
   viPortalTwoDestFineRow = 44
   viPortalTwoDestFineCol = 38
   viPortalTwoDestAngle = ANGLE_SOUTH_EAST
   vsPortalTwoDesc = jasper_tomb_portal_desc
   
   viPortalThreeRow = 30
   viPortalThreeCol = 16
   viPortalThreeFineRow = 32
   viPortalThreeFineCol = 0
   viPortalThreeAngle = ANGLE_WEST
   viPortalThreeDestRID = 0
   viPortalThreeDestRow = 1
   viPortalThreeDestCol = 9
   viPortalThreeDestFineRow = 31
   viPortalThreeDestFineCol = 25
   viPortalThreeDestAngle = ANGLE_SOUTH_WEST
   vsPortalThreeDesc = jasper_tomb_portal_desc
   
   viPortalFourRow = 28
   viPortalFourCol = 22
   viPortalFourFineRow = 32
   viPortalFourFineCol = 32
   viPortalFourAngle = ANGLE_NORTH
   viPortalFourDestRID = 0
   viPortalFourDestRow = 5
   viPortalFourDestCol = 7
   viPortalFourDestFineRow = 40
   viPortalFourDestFineCol = 25
   viPortalFourDestAngle = ANGLE_WEST
   vsPortalFourDesc = jasper_tomb_portal_desc
   
   viPortalFiveRow = 34
   viPortalFiveCol = 29
   viPortalFiveFineRow = 10
   viPortalFiveFineCol = 18
   viPortalFiveAngle = ANGLE_NORTH
   viPortalFiveDestRID = 0
   viPortalFiveDestRow = 30
   viPortalFiveDestCol = 2
   viPortalFiveDestFineRow = 2
   viPortalFiveDestFineCol = 59
   viPortalFiveDestAngle = ANGLE_NORTH_EAST
   vsPortalFiveDesc = jasper_tomb_portal_desc
   
   viPortalSixRow = 18
   viPortalSixCol = 18
   viPortalSixFineRow = 0
   viPortalSixFineCol = 0
   viPortalSixAngle = ANGLE_NORTH
   viPortalSixDestRID = 0
   viPortalSixDestRow = 34
   viPortalSixDestCol = 6
   viPortalSixDestFineRow = 29
   viPortalSixDestFineCol = 58
   viPortalSixDestAngle = ANGLE_NORTH_EAST
   vsPortalSixDesc = jasper_tomb_portal_desc
   
   viReliquaryOneRow = 3
   viReliquaryOneCol = 29
   viReliquaryOneFineRow = 32
   viReliquaryOneFineCol = 0
   
   viReliquaryTwoRow = 23
   viReliquaryTwoCol = 7
   viReliquaryTwoFineRow = 32
   viReliquaryTwoFineCol = 0
   
   viReliquaryThreeRow = 30
   viReliquaryThreeCol = 14
   viReliquaryThreeFineRow = 32
   viReliquaryThreeFineCol = 32
   
   viReliquaryFourRow = 27
   viReliquaryFourCol = 22
   viReliquaryFourFineRow = 0
   viReliquaryFourFineCol = 32
   
   viReliquaryFiveRow = 32
   viReliquaryFiveCol = 29
   viReliquaryFiveFineRow = 46
   viReliquaryFiveFineCol = 14
   
   viReliquarySixRow = 16
   viReliquarySixCol = 18
   viReliquarySixFineRow = 32
   viReliquarySixFineCol = 0
   
   % Fusing device rates go from 8 (worst) to 4 (best) 
   viShardFusingDeviceRow = 13
   viShardFusingDeviceCol = 27
   viShardFusingDeviceFineRow = 0
   viShardFusingDeviceFineCol = 0
   viShardFusingDeviceRate = 4
   
   % Shattering device rates go from 2 (worst) to 4 (best) 
   viShardShatteringDeviceRow = 12
   viShardShatteringDeviceCol = 31
   viShardShatteringDeviceFineRow = 32
   viShardShatteringDeviceFineCol = 32
   viShardShatteringDeviceRate = 4
   
   viFociOneRow = 15
   viFociOneCol = 29
   viFociOneFineRow = 0
   viFociOneFineCol = 0
   viFociOnePower = 150
   viFociOneRange = 5
   viFociOneSpellNum = SID_REJUVENATE
   
   viFociTwoRow = 15
   viFociTwoCol = 29
   viFociTwoFineRow = 0
   viFociTwoFineCol = 0
   viFociTwoPower = 150
   viFociTwoRange = 5
   viFociTwoSpellNum = SID_RESTORATE

properties:

   prMusic = jasper_tomb_music
   prRoom = room_jaspertomb
   piRoom_num = RID_JASPER_TOMB

   piBaseLight = LIGHT_MIN
   piOutside_Factor = OUTDOORS_NONE

   ptEntrance = $
   ptSecretDoor = $

   plGuild_doors
   
   % Jasper Tomb boosts all skill and spell meditation by 40%.
   piGuildHallMeditateEffiency = 140

messages:

   Constructor()
   "These mark the exterior door of the guild, which is only openable by "
   "a guild member or an allied guild member."
   {
      plGuild_doors = [[6,14], [6,15], [6,16], [6,17], [6,18],
                       [7,15], [7,16], [7,17],
                       [8,15], [8,16], [8,17],
                       [9,15], [9,16], [9,17],
                       [10,15], [10,16], [10,17],
                       [11,15], [11,16], [11,17],
                       [12,15], [12,16], [12,17],
                       [13,15], [13,16], [13,17]
      ];

      % This is a box containing the guild door, used
      % for removing players from it when it closes.
      plEntrance = [ 1, 2, 1, 2 ];

      propagate;
   }
   
   FirstUserEntered()
   {
      Post(self,@PlaceSpectrals);
      Post(self,@CheckPortals);
      propagate;
   }

   LastUserLeft()
   {
      Post(self,@DeleteSpectrals);
      propagate;
   }

   CheckPortals()
   {
      local i, iRow;
      SendListByClass(plActive,1,&Portal,@SetAnimation,#what=FALSE);

      if poGuild_owner <> $
      {
         foreach i in plActive
         {
            if IsClass(First(i),&Reliquary)
            {
               if Send(First(i),@GetActive)
               {
                  iRow = Send(First(i),@GetRow);
                  switch(iRow)
                  {
                     case 3:
                        Send(self,@SetUpPortal,#portal_at_row=2,#oItem=Send(First(i),@GetHolderPassive));
                        break;
                     case 23:
                        Send(self,@SetUpPortal,#portal_at_row=22,#oItem=Send(First(i),@GetHolderPassive));
                        break;
                     case 30:
                        Send(self,@SetUpPortal,#portal_at_row=30,#oItem=Send(First(i),@GetHolderPassive));
                        break;
                     case 27:
                        Send(self,@SetUpPortal,#portal_at_row=28,#oItem=Send(First(i),@GetHolderPassive));
                        break;
                     case 32:
                        Send(self,@SetUpPortal,#portal_at_row=34,#oItem=Send(First(i),@GetHolderPassive));
                        break;
                     case 16:
                        Send(self,@SetUpPortal,#portal_at_row=18,#oItem=Send(First(i),@GetHolderPassive));
                        break;
                  }
               }
            }
         }
      }

      return;
   }

   SetUpPortal(portal_at_row=0, oItem = $)
   {
      local i, each_obj, oPortal, iDestRID, cItemClass;
      
      oPortal = $;
      foreach i in Send(self,@GetHolderActive)
      {
         each_obj = Send(self,@HolderExtractObject,#data=i);
         if IsClass(each_obj,&Portal)
            AND Send(each_obj,@GetRow) = portal_at_row
         {
            oPortal = each_obj;
            break;
         }
      }

      oItem = First(oItem);

      if oPortal <> $
         AND oItem <> $
      {
         cItemClass = GetClass(oItem);
         switch(cItemClass)
         {
            case &MajorRedHeartstone:
               Send(oPortal,@SetDestination,#dest_room_num=RID_DESERTRIVER2,
                                            #dest_angle=ANGLE_NORTH,
                                            #dest_row=35,
                                            #dest_col=24,
                                            #dest_fine_row=48,
                                            #dest_fine_col=10,
                                            #glow_color=LIGHT_BRED);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &MajorBlueHeartstone:
               Send(oPortal,@SetDestination,#dest_room_num=RID_DESERTSANDBAR,
                                            #dest_angle=ANGLE_SOUTH_WEST,
                                            #dest_row=53,
                                            #dest_col=14,
                                            #dest_fine_row=45,
                                            #dest_fine_col=29,
                                            #glow_color=LIGHT_BBLUE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &MajorGreenHeartstone:
               Send(oPortal,@SetDestination,#dest_room_num=RID_ACIDPATH,
                                            #dest_angle=ANGLE_SOUTH_WEST,
                                            #dest_row=86,
                                            #dest_col=133,
                                            #dest_fine_row=60,
                                            #dest_fine_col=58,
                                            #glow_color=LIGHT_BGREEN);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &MajorSkyHeartstone:
               Send(oPortal,@SetDestination,#dest_room_num=RID_DESERTBRIDGE2,
                                            #dest_angle=ANGLE_EAST,
                                            #dest_row=10,
                                            #dest_col=10,
                                            #dest_fine_row=32,
                                            #dest_fine_col=25,
                                            #glow_color=LIGHT_LIGHTNING);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &MajorBlackHeartStone:
               Send(oPortal,@SetDestination,#dest_room_num=RID_DESERTALDUNES4E,
                                            #dest_angle=ANGLE_EAST,
                                            #dest_row=53,
                                            #dest_col=14,
                                            #dest_fine_row=45,
                                            #dest_fine_col=29,
                                            #glow_color=LIGHT_BPURPLE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &MajorWhiteHeartStone:
               Send(oPortal,@SetDestination,#dest_room_num=RID_DESERTALDUNES4W,
                                            #dest_angle=ANGLE_WEST,
                                            #dest_row=53,
                                            #dest_col=14,
                                            #dest_fine_row=45,
                                            #dest_fine_col=29,
                                            #glow_color=LIGHT_BWHITE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &MajorBrownHeartStone:
               Send(oPortal,@SetDestination,#dest_room_num=RID_K5,
                                            #dest_angle=ANGLE_WEST,
                                            #dest_row=39,
                                            #dest_col=38,
                                            #dest_fine_row=34,
                                            #dest_fine_col=24,
                                            #glow_color=LIGHT_WHITE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &MajorPurpleHeartStone:
               Send(oPortal,@SetDestination,#dest_room_num=RID_KE1,
                                            #dest_angle=3787,
                                            #dest_row=31,
                                            #dest_col=4,
                                            #dest_fine_row=29,
                                            #dest_fine_col=41,
                                            #glow_color=LIGHT_BPURPLE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &MajorDarkGreenHeartStone:
               Send(oPortal,@SetDestination,#dest_room_num=RID_ORC_PIT,
                                            #dest_angle=ANGLE_NORTH,
                                            #dest_row=31,
                                            #dest_col=41,
                                            #dest_fine_row=32,
                                            #dest_fine_col=32,
                                            #glow_color=LIGHT_BGREEN);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &FarenCharm:
               Send(oPortal,@SetDestination,#dest_room_num=RID_BADLAND1,
                                            #dest_angle=3728,
                                            #dest_row=58,
                                            #dest_col=33,
                                            #dest_fine_row=57,
                                            #dest_fine_col=15,
                                            #glow_color=LIGHT_FIRE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &KraananCharm:
               Send(oPortal,@SetDestination,#dest_room_num=RID_TEMPLE_KRAANAN,
                                            #dest_angle=3864,
                                            #dest_row=8,
                                            #dest_col=1,
                                            #dest_fine_row=10,
                                            #dest_fine_col=61,
                                            #glow_color=LIGHT_BLUE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &ShalilleCharm:
               Send(oPortal,@SetDestination,#dest_room_num=RID_TEMPLE,
                                            #dest_angle=ANGLE_EAST,
                                            #dest_row=24,
                                            #dest_col=8,
                                            #dest_fine_row=47,
                                            #dest_fine_col=31,
                                            #glow_color=LIGHT_BWHITE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &QorCharm:
               Send(oPortal,@SetDestination,#dest_room_num=RID_GUILDH15,
                                            #dest_angle=ANGLE_WEST,
                                            #dest_row=3,
                                            #dest_col=13,
                                            #dest_fine_row=16,
                                            #dest_fine_col=57,
                                            #glow_color=LIGHT_BRED);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &RiijaCharm:
               Send(oPortal,@SetDestination,#dest_room_num=RID_KD1,
                                            #dest_angle=ANGLE_SOUTH_EAST,
                                            #dest_row=12,
                                            #dest_col=22,
                                            #dest_fine_row=48,
                                            #dest_fine_col=36,
                                            #glow_color=LIGHT_PURPLE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &MagicBow:
               Send(oPortal,@SetDestination,#dest_room_num=RID_KC3,
                                            #dest_angle=110,
                                            #dest_row=22,
                                            #dest_col=2,
                                            #dest_fine_row=34,
                                            #dest_fine_col=41,
                                            #glow_color=LIGHT_GREEN);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &NeruditeBow:
               Send(oPortal,@SetDestination,#dest_room_num=RID_SEWER_KING,
                                            #dest_angle=ANGLE_NORTH_WEST,
                                            #dest_row=42,
                                            #dest_col=48,
                                            #dest_fine_row=8,
                                            #dest_fine_col=37,
                                            #glow_color=LIGHT_GREEN);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &GoldSword:
               Send(oPortal,@SetDestination,#dest_room_num=RID_MAR_CRYPT3A,
                                            #dest_angle=ANGLE_SOUTH_EAST,
                                            #dest_row=30,
                                            #dest_col=20,
                                            #dest_fine_row=44,
                                            #dest_fine_col=38,
                                            #glow_color=LIGHT_SPECTRAL);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &Helm:
               Send(oPortal,@SetDestination,#dest_room_num=RID_FIELD1,
                                            #dest_angle=3315,
                                            #dest_row=43,
                                            #dest_col=6,
                                            #dest_fine_row=42,
                                            #dest_fine_col=11,
                                            #glow_color=LIGHT_LIGHTNING);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &DefenseArmorRelic:
               Send(oPortal,@SetDestination,#dest_room_num=RID_TOS,
                                            #dest_angle=ANGLE_SOUTH_EAST,
                                            #dest_row=46,
                                            #dest_col=10,
                                            #dest_fine_row=61,
                                            #dest_fine_col=27,
                                            #glow_color=LIGHT_WHITE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &MeleeArmorRelic:
               Send(oPortal,@SetDestination,#dest_room_num=RID_CORNOTH,
                                            #dest_angle=ANGLE_NORTH,
                                            #dest_row=45,
                                            #dest_col=19,
                                            #dest_fine_row=11,
                                            #dest_fine_col=18,
                                            #glow_color=LIGHT_WHITE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &OffenseArmorRelic:
               Send(oPortal,@SetDestination,#dest_room_num=RID_JASPER,
                                            #dest_angle=2681,
                                            #dest_row=46,
                                            #dest_col=56,
                                            #dest_fine_row=60,
                                            #dest_fine_col=31,
                                            #glow_color=LIGHT_WHITE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &PunchArmorRelic:
               Send(oPortal,@SetDestination,#dest_room_num=RID_BAR_NORTH,
                                            #dest_angle=ANGLE_EAST,
                                            #dest_row=4,
                                            #dest_col=41,
                                            #dest_fine_row=60,
                                            #dest_fine_col=6,
                                            #glow_color=LIGHT_WHITE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &RangedArmorRelic:
               Send(oPortal,@SetDestination,#dest_room_num=RID_MARION,
                                            #dest_angle=ANGLE_SOUTH,
                                            #dest_row=15,
                                            #dest_col=22,
                                            #dest_fine_row=57,
                                            #dest_fine_col=32,
                                            #glow_color=LIGHT_WHITE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
            case &ExperienceRelic:
               Send(oPortal,@SetDestination,#dest_room_num=RID_KOCATAN,
                                            #dest_angle=ANGLE_WEST,
                                            #dest_row=19,
                                            #dest_col=56,
                                            #dest_fine_row=49,
                                            #dest_fine_col=52,
                                            #glow_color=LIGHT_WHITE);
               Send(oPortal,@SetAnimation,#what=TRUE);
               break;
         }
      }
      
      return;
   }

   PlaceSpectrals()
   {
      local iCount;
      
      iCount = 15;
      
      while iCount > 0
      {
         Send(self,@NewHold,#what=Create(&DynamicLight,#iColor=LIGHT_SPECTRAL,
           #iIntensity=80),#new_row=Random(5,Send(self,@GetRoomRows)-5),#new_col=Random(5,Send(self,@GetRoomCols)-5));
         iCount--;
      }

      return;
   }

   DeleteSpectrals()
   {
      local i, each_obj;
      
      foreach i in Send(self,@GetHolderPassive)
      {
         each_obj = Send(self,@HolderExtractObject,#data=i);
         if IsClass(each_obj,&DynamicLight)
            AND Send(each_obj,@GetColor) = LIGHT_SPECTRAL
         {
            Post(each_obj,@Delete);
         }
      }
      return;
   }

   OpenEntranceDoor()
   {
      if ptEntrance = $
      {
         Send(self,@SomethingWaveRoom,#wave_rsc=jaspertomb_sound);
         Send(self,@SetSector,#sector=ENTRANCE_DOOR,#animation=ANIMATE_FLOOR_LIFT,#height=500,#speed=50);
         Send(self,@TimeStampDoor);
         ptEntrance = CreateTimer(self,@CloseEntranceDoor,ENTRANCE_CLOSE_TIME);
      }

      return;
   }

   OpenSecretDoor()
   {
      if ptSecretDoor = $
      {
         Send(self,@SomethingWaveRoom,#wave_rsc=jaspertomb_sound);
         Send(self,@SetSector,#sector=SECRET_DOOR,#animation=ANIMATE_CEILING_LIFT,#height=690,#speed=45);
         ptSecretDoor = CreateTimer(self,@CloseSecretDoor,SECRET_CLOSE_TIME);
      }

      return;
   }

   CloseEntranceDoor()
   {
      ptEntrance = $;
      Send(self,@SomethingWaveRoom,#wave_rsc=jaspertomb_sound);
      Send(self,@SetSector,#sector=ENTRANCE_DOOR,#animation=ANIMATE_FLOOR_LIFT,#height=0,#speed=50);
      Send(self,@TimeStampDoor);
      
      return;
   }

   CloseSecretDoor()
   {
      ptSecretDoor = $;
      Send(self,@SomethingWaveRoom,#wave_rsc=jaspertomb_sound);
      Send(self,@SetSector,#sector=SECRET_DOOR,#animation=ANIMATE_CEILING_LIFT,#height=600,#speed=45);

      return;
   }

   Delete()
   {
      if ptEntrance <> $
      {
         DeleteTimer(ptEntrance);
         ptEntrance = $;
      }

      if ptSecretDoor <> $
      {
         DeleteTimer(ptSecretDoor);
         ptSecretDoor = $;
      }

      propagate;
   }

   SomethingTryGo(what = $,row = $,col = $,fine_row=$,fine_col=$)
   {
      if (row >= 13 AND fine_row >= 32)
         AND (row <= 15)
         AND (col >= 14 AND fine_col >= 32)
         AND (col <= 19)
      {
         Send(self,@OpenEntranceDoor);

         return TRUE;
      }

      propagate;
   }

   CreateStandardObjects()
   {
      Send(self,@NewHold,#what=Create(&Table),#new_row=13,#new_col=28);

      Send(self,@NewHold,#what=Create(&Chest),
            #new_row=28,#new_col=27,#fine_row=32,#fine_col=0,
            #new_angle=ANGLE_EAST);
      Send(self,@NewHold,#what=Create(&Chest),
            #new_row=28,#new_col=31,#fine_row=32,#fine_col=0,
            #new_angle=ANGLE_WEST);
      Send(self,@NewHold,#what=Create(&WoodenBox),#new_row=18,#new_col=31,
            #fine_row=0,#fine_col=0,#new_angle=ANGLE_NORTH_WEST);

      Send(self,@NewHold,#what=Create(&Brazier),#new_row=10,#new_col=14,
            #fine_row=32,#fine_col=32);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=4,#new_col=7,
            #fine_row=0,#fine_col=32);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=4,#new_col=32,
            #fine_row=0,#fine_col=0);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=24,#new_col=3,
            #fine_row=0,#fine_col=0);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=26,#new_col=25,
            #fine_row=32,#fine_col=0);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=32,#new_col=14,
            #fine_row=32,#fine_col=0);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=15,#new_col=5,
            #fine_row=24,#fine_col=2);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=9,#new_col=3,
            #fine_row=10,#fine_col=44);
            
            
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=1,#new_col=18,
            #fine_row=48,#fine_col=0);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=1,#new_col=14,
            #fine_row=48,#fine_col=32);
            
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=12,#new_col=27,
            #fine_row=8,#fine_col=11);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=18,#new_col=31,
            #fine_row=26,#fine_col=28);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=14,#new_col=31,
            #fine_row=15,#fine_col=51);
            
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=27,#new_col=31,
            #fine_row=40,#fine_col=48);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=27,#new_col=26,
            #fine_row=40,#fine_col=48);
      propagate;
   }

   InFoyer(who=$,iRow=$,iCol=$,iFineRow=$,iFineCol=$)
   {
      local iSection;

      if who <> $
      {
         iRow = Send(who,@GetRow);
         iCol = Send(who,@GetCol);
         iFineRow = Send(who,@GetFineRow);
         iFineCol = Send(who,@GetFineCol);
      }

      iSection = Send(self,@GetSection,#who=who,#iRow=iRow,#iCol=iCol,#iFineRow=iFineRow,#iFineCol=iFineCol);
      if iSection = AREA_INSIDE
      {
         return FALSE;
      }

      return TRUE;
   }

   GetSection(who=$,iRow=$,iCol=$,iFineRow=$,iFineCol=$,bCheckForPool=FALSE)
   {
      local iQflags, iRflags, iHeightF, iHeightFWD, iHeightC, iServerID;
      
      if iRow = $
         OR iCol = $
         OR iFineRow = $
         OR iFineCol = $
      {
         if who <> $
         {
            iRow = Send(who,@GetRow);
            iCol = Send(who,@GetCol);
            iFineRow = Send(who,@GetFineRow);
            iFineCol = Send(who,@GetFineCol);
         }
         else
         {
            return AREA_OUTSIDE;
         }
         if iRow = $
            OR iCol = $
            OR iFineRow = $
            OR iFineCol = $
         {
            % Who gave us some nil coords.
            return AREA_OUTSIDE;
         }
      }

      iQflags = LIQ_GET_SECTORINFO;

      if GetLocationInfoBSP(
                      prmRoom, iQflags, iRow, iCol, iFineRow, iFineCol,
                      *iRflags, *iHeightF, *iHeightFWD, *iHeightC, *iServerID)
      {
         if iServerID = INSIDE_SECTIONS
            OR iServerID = SECRET_DOOR
         {
            return AREA_INSIDE;
         }
      }

      return AREA_OUTSIDE;
   }

   CreateStandardExits()
   {
      plExits = $;

      %% TO TOWN
      plExits = Cons([ 2, 15, RID_JASWEST, 22, 24, ROTATE_NONE ],plExits);
      plExits = Cons([ 2, 16, RID_JASWEST, 22, 25, ROTATE_NONE ],plExits);
      plExits = Cons([ 2, 17, RID_JASWEST, 22, 26, ROTATE_NONE ],plExits);
      plExits = Cons([ 1, 15, RID_JASWEST, 22, 24, ROTATE_NONE ],plExits);
      plExits = Cons([ 1, 16, RID_JASWEST, 22, 25, ROTATE_NONE ],plExits);
      plExits = Cons([ 1, 17, RID_JASWEST, 22, 26, ROTATE_NONE ],plExits);

      propagate;
   }
   
   SomethingChanged(what=$)
   {
      if what <> $
         AND IsClass(what,&Reliquary)
      {
         Post(self,@CheckPortals);
      }

      propagate;
   }

   ReqUseGuildChest(who=$,oBox=$)
   {
      if oBox <> $
         AND IsClass(oBox,&Reliquary)
         AND Send(oBox,@GetRow) = 3
      {
         return TRUE;
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
