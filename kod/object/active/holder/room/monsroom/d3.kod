% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
OutdoorsD3 is MonsterRoom

constants:

   include blakston.khd

   % 26, 44 for central rock

resources:

   room_name_OutdoorsD3 = "Shipwreck Reef"
   room_OutdoorsD3 = d3.roo
   OutdoorsD3_music = walk7.mp3

   D3_oceanwalk = \
      "You decide you don't like saltwater enough to go swimming "
      "in the Great Ocean."
   D3_icewalk = "You walk back toward the shore."

classvars:

   vrName = room_name_OutdoorsD3

   viTeleport_row = 49
   viTeleport_col = 81

   viLongitude = 7
   viLatitude = 3

   viTerrain_type = TERRAIN_BEACH | TERRAIN_LAKE | TERRAIN_RIVER

   viWeatherZone = WEATHER_ZONE_BARLOQUE

properties:

   prRoom = room_OutdoorsD3
   prMusic = OutdoorsD3_music
   piRoom_num = RID_D3

   piBaseLight = LIGHT_NICE
   piOutside_factor = 8

   piDirectional_percent = DIRECTIONAL_PERCENT_OUTDOORS

   pbSnowGroundTexture = TRUE

   piGen_time = 20000
   piGen_percent = 70

   piInit_count_min = 2
   piInit_count_max = 5
   
   ptSecretTimer = $

messages:

   Constructed()
   {
%      plMonsters = [ [&MolluskMonster, 100] ];

%      plGenerators = [ [40, 6], [45, 13], [58, 28], [34, 34], [32, 53],
%                       [53, 50], [40, 67], [14, 66], [15, 46], [4, 68] ];

      Post(self,@CreateReward);

      propagate;
   }

   CreateStandardExits()
   {
      plEdge_Exits = $;
      plEdge_Exits = Cons([LEAVE_SOUTH, RID_E1, 10, 16, ROTATE_NONE], plEdge_exits);

      propagate;
   }
   
   CreateReward()
   {
      local i;
      
      ptSecretTimer = $;
      return;
      
      foreach i in Send(self,@GetHolderActive)
      {
         if IsClass(First(i),&AlchemySkillBook)
            OR IsClass(First(i),&AlchemySpellBook)
         {
            ptSecretTimer = CreateTimer(self,@CreateReward,60000 * 60 * 6 + Random(-300000,300000));
            return;
         }
      }
      if Random(1,2) = 1
      {
         Send(self,@NewHold,#what=Create(&AlchemySkillBook),#new_row=25,#new_col=45,#fine_row=29,#fine_col=4);
      }
      else
      {
         Send(self,@NewHold,#what=Create(&AlchemySpellBook),#new_row=25,#new_col=45,#fine_row=29,#fine_col=4);
      }
      ptSecretTimer = CreateTimer(self,@CreateReward,60000 * 60 * 6 + Random(-300000,300000));
      return;
   }

   StandardLeaveDir(what = $,dir = $,pos = $,new_room_num = $)
   {
      local oTargetRoom;

      if (dir = LEAVE_SOUTH)
      {
         if (IsClass(what,&Player))
         {
         %   if Send(self,@CheckRoomFlag,#flag=ROOM_SNOWING)
         %      AND pbSnowGroundTexture
         %   {
               oTargetRoom = Send(SYS,@FindRoomByNum,#num=RID_E1);
         %      Send(what,@MsgSendUser,#message_rsc=D3_icewalk);
               Send(SYS,@UtilGoNearSquare,#what=what,
                    #where=oTargetRoom,
                    #new_row=4,
                    #new_col=23,
                    #fine_row=0,
                    #fine_col=0,
                    #new_angle=Send(what,@GetAngle));
         %   }
         %   else
         %   {
         %      Send(what,@MsgSendUser,#message_rsc=D3_oceanwalk);
         %   }
         }

         return;
      }
      else
      {
         Send(what,@MsgSendUser,#message_rsc=D3_oceanwalk);
         return;
      }

      propagate;
   }

   CreateStandardObjects()
   {
	   Send(self,@NewHold,#what=Create(&OrnamentalObject,#type=OO_BONES),#new_row=25,#new_col=42,#fine_row=29,#fine_col=9,#angle=ANGLE_EAST);

      propagate;
   }
   
   Delete()
   {
      if ptSecretTimer <> $
      {
         DeleteTimer(ptSecretTimer);
         ptSecretTimer = $;
      }
      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
