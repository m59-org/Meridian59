% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
PrincessLiege is Factions

constants:

   include blakston.khd

resources:

   include princess.lkod

   princess_name_rsc = "Princess Kateriina"
   princess_icon_rsc = princess.bgf
   princess_desc_rsc = \
      "Age has made Princess Kateriina stronger and more certain of herself, "
      "but has done little to temper her naivete.  She has developed great "
      "disdain for the Duke and all who engage in political manoeuvring.  She "
      "considers matters of political intrigue to be below her, trusting "
      "that if she rules wisely and justly all will accept her.  Magic "
      "continues to fascinate her and she sometimes dreams of running away "
      "to study the arcane arts."

   princess_token_delivered_rsc = \
      "Ahhh.  I will ensure that this gets returned to its rightful "
      "councillor.  By my crown, he will show me favor. %s"
   princess_princess_delivered_rsc = "Thou wast ever a loyal follower."
   princess_neutral_delivered_rsc = \
      "Since thou possess a like mind, please support my faction.  Put thy "
      "name to the roll hither..."
   princess_other_delivered_rsc = \
      "Amen.  Even the servants of the pretenders to the throne start to "
      "see the better path.  Let us change thee over to our faction, shall we?"
   princess_toopowered_rsc = \
      "In faith, if we were not already a very powerful faction, I would "
      "offer to have thee join us."

   heartstone_delivered_rsc = \
      "Indeed, you are Meridian's best hope against this threat. You and any "
      "companions you deem fit may descend to the basement of this castle and "
      "use the portal my mages have established. It will take you to the source "
      "of the Xeochicatl threat, but, beyond that, we know little."

classvars:

   viQuestID = QST_ID_PRINCESS_SERVICE
   viGender = GENDER_FEMALE
   viFaction = FACTION_PRINCESS
   viKarma = 0
   viAttributes = \
      MOB_NOFIGHT | MOB_LISTEN | MOB_RANDOM | MOB_RECEIVE \
      | MOB_NOMOVE | MOB_PERM_QUESTER | MOB_TEACHER
   viOccupation = MOB_ROLE_ROYALTY

   viHomeroom = RID_CASTLE2C
   viMyRow = 33
   viMyCol = 23
   viMyAngle = ANGLE_NORTH
   viMyFR = 16
   viMyFC = 16

properties:

   vrName = princess_name_rsc
   vrIcon = princess_icon_rsc
   vrDesc = princess_desc_rsc


messages:

   AffectMood(why=$,what=$)
   {
      if why = MOODMOD_FACTION_CHANGE
      {
         if what = FACTION_DUKE
         {
            Send(self,@SetMood,#new_mood=piMood-50);
         }

         if what = FACTION_PRINCESS
         {
            Send(self,@SetMood,#new_mood=piMood+50);
         }
      }

      return;
   }

   Constructor()
   {
      plWantedItems=[&Token, &Heartstone];

      propagate;
   }

   CheckWhyWanted(obj=$,who=$)
   {
      local temp_rsc;
      if isClass(obj,&Token) AND Send(SYS,@GetTokenGame) <> $
      AND Send(Send(SYS,@GetTokenGame),@IsTokenReal,#obj=obj)
      {
         if Send(who,@GetFaction)=FACTION_PRINCESS
         {
            temp_rsc=princess_princess_delivered_rsc;
         }

         if Send(who,@GetFaction)=FACTION_NEUTRAL
         {
            temp_rsc=princess_neutral_delivered_rsc;
         }

         if Send(who,@GetFaction)=FACTION_DUKE
         {
            temp_rsc=princess_other_delivered_rsc;
         }

         if (Send(Send(SYS,@GetParliament),@GetPower,
                  #faction=FACTION_PRINCESS) = FACTION_STRONGLY_IN)
            AND Send(who,@GetFaction) <> FACTION_PRINCESS
         {
            temp_rsc=princess_toopowered_rsc;
         }

         Send(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
               #string=princess_token_delivered_rsc,#parm1=temp_rsc);

         return TRUE;
      }

      if IsClass(obj,&Heartstone)
      {
         Send(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
               #string=heartstone_delivered_rsc);
         
         %Send(Send(SYS,@FindRoomByNum,#num=RID_MOCKERS_ROOM),@OpenPortalToBiskalane);

         return TRUE;
      }

      propagate;
   }

   SetForSale()
   {
      plFor_Sale = [$,
                    [SKID_SKITTERATIC_LANGUAGE,
                     SKID_SYLVAN_LANGUAGE,
                     SKID_AMPHIBIC_LANGUAGE,
                     SKID_WILLPOWER,
                     SKID_BIRDSONG_LANGUAGE,
                     SKID_ORCISH_LANGUAGE,
                     SKID_PRIMAL_LANGUAGE,
                     SKID_TROLLIMAIC_LANGUAGE,
                     SKID_SOUL_DOMINATION,
                     SKID_DRACONIC_LANGUAGE],
                    [ SID_CONCILIATION,
                      SID_SUMMON_ABOMINATION,
                      SID_SUMMON_WOLF,
                      SID_SUMMON_ELEPHANT,
                      SID_SUMMON_ELEMENTAL,
                      SID_MARK_OF_AGGRESSION,
                      SID_STUDY_CREATURE_SPELLS,
                      SID_RELEASE_MINION,
                      SID_CHARM_INSECT,
                      SID_CREATURE_SPEAK,
                      SID_CHARM_PLANT,
                      SID_STUDY_CREATURE_RESISTS,
                      SID_SOOTHING_PRESENCE,
                      SID_TOWERING_PRESENCE,
                      SID_MARK_OF_DEFENSE,
                      SID_CREATURE_BINDING,
                      SID_WATER_BREATHING,
                      SID_CONVOKE,
                      SID_CHARM_ANIMAL,
                      SID_GIFT_OF_VITALITY,
                      SID_GIFT_OF_SPIRIT,
                      SID_CHARM_UNDEAD,
                      SID_CHARM_HUMANOID,
                      SID_MARK_OF_MAGIC,
                      SID_CHARM_ELEMENTAL] ];

      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
