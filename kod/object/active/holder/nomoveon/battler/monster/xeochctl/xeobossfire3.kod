% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
XeoBossFire3 is Xeochicatl

constants:

   include blakston.khd

   ANIM_CAST = 2     %% Cast spell animation.

resources:

   XeoBossFire3_name_rsc = "vix'ro'xeochicatl"
   XeoBossFire3_koc_name_rsc = "vix'ro'xeo'chicatl"
   XeoBossFire3_icon_rsc = Xeo.bgf
   XeoBossFire3_desc_rsc = \
      "Before you is a vix'ro'xeochicatl, a being from near the heart of the "
      "elemental plane of fire. This is no minor xeochicatl; the vix are "
      "far too strong to grace the mortal plane without visiting tremendous "
      "destruction. This beast is an elemental incarnation of fire itself."

   XeoBossFire3_sound_aware = xeo3awr.wav

classvars:

   vrIcon = biggest_Xeochicatl_icon_rsc
   vrDead_icon = biggest_Xeochicatl_dead_icon_rsc

   vrName = XeoBossFire3_name_rsc
   vrDesc = XeoBossFire3_desc_rsc
   vrKocName = XeoBossFire3_koc_name_rsc

   viAttack_type = ATCK_WEAP_MAGIC
   viLevel = 200
   viDifficulty = 10
   viSpeed = SPEED_VERY_FAST
   vrSound_aware = XeoBossFire3_sound_aware

   viTreasure_type = TID_XEO_BOSS_FIRE

   viBonusBaseTP = 5000
   
   viBaseSize = 1800
   
   viEnemyRangeBonus = 64 * 4

   vbBonusXPGain = 5000

properties:

   piAnimation = ANIM_NONE
   piDrawEffectFlag = OF_FLICKERING
   piDrawfx = DRAWFX_TRANSLUCENT_75
   piMinDamage = 16
   piMaxDamage = 40

   piMax_Mana = 100

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_FIRE, 200 ],
                        [-ATCK_SPELL_COLD, 60 ],
                        [-ATCK_SPELL_SHOCK, 100 ],
                        [-ATCK_SPELL_ACID, 100 ],
                        [-ATCK_SPELL_HOLY, 100 ],
                        [-ATCK_SPELL_UNHOLY, 100 ],
                        [ATCK_WEAP_MAGIC, -10 ],
                        [ATCK_WEAP_ALL,60],
                        [-ATCK_SPELL_QUAKE, 100 ]
                      ];

      % Permanent eagle eyes and free action, purge
      Post(self,@AddChampionAbility,#ability=3,#value=80);
      Post(self,@AddChampionAbility,#ability=4,#value=80);
      Post(self,@AddChampionAbility,#ability=6,#value=15);

      piColor_Translation = Send(SYS,@EncodeTwoColorXLAT,#color1=XLAT_TO_RED);

      plMeleeSpellBook = [  [SID_FIREWALL,10,10, Random(70,99)],
                            [SID_BLAST_OF_FIRE,3,100, Random(70,99)] ];
      plRangedSpellBook = [ [SID_FIREBALL,90, Random(70,99)],
                            [SID_FIRE_STORM,10,100, Random(70,99)] ];


      propagate;
   }

   GetMaxHitPoints()
   {
      return 5000;
   }

   GetBossStatus()
   {
      return TRUE;
   }

   SendLightingInformation()
   {
      AddPacket(2,(LIGHT_FLAG_ON | LIGHT_FLAG_DYNAMIC));
      % 255 out of 255 intensity of light
      AddPacket(1,255);
      AddPacket(2,LIGHT_BRED);

      return;
   }

   IsAlly(target = $, regroup = FALSE)
   {
      if IsClass(target,&XeoBossFire)
         OR IsClass(target,&XeoBossFire2)
         OR IsClass(target,&XeoBossFire3)
         OR IsClass(target,&XeoFire)
         OR IsClass(target,&FireElemental)
         OR IsClass(target,&FireElementalChampion)
      {
         return TRUE;
      }

      propagate;
   }

   Killed()
   {
      local i;
      
      foreach i in plHurtMeRecently
      {
         if IsClass(First(i),&Player)
         {
            Send(First(i),@SetPrismDefeated,#num=NTH_FIRE_PRISM,#value=1);
         }
      }
      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
