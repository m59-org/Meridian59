% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
OneCouncilor is Council

constants:

   include blakston.khd
   NUM_SPELLS_FOR_SPECIAL = 16

resources:

   include onecon.lkod

   oneCouncilor_name_rsc = "Alzahakar"
   oneCouncilor_icon_rsc = onecon.bgf
   oneCouncilor_desc_rsc = \
      "Renowned as the most powerful wizard in the Kingdom, Alzahakar stands "
      "proudly before you.  You can feel the conflicting will of four separate "
      "gods within him.  As one of the oldest friends of King Zarcos V, he has "
      "been counselling the princess since birth."
      "\n\nIf you are a student with room to grow, he may help you improve "
      "your intellect if you ask him to 'train' you."

   onecon_true_ideal_rsc = \
      "I have great respect for your knowledge of magic, and having you "
      "return my token has affected me greatly."
   onecon_half_ideal_rsc = \
      "I would be more impressed if you knew more spells, yet you "
      "have potential."

   onecon_not_ready = "You are not yet ready to learn the lost arts."
   onecon_already_has = "You already know of the lost secrets of Riija."

   onecon_unwanted_give = \
      "~k%s%s tells you, \"What possible use could a wizard of my stature "
      "have for that?~n~k\""

   major_shard_delivered = \
      "Thank you. This will help greatly in my research."

   cannot_train_intellect_msg = \
      "You seem to have full command of your mind's potential. I cannot train you further."
   teach_intellect_msg = \
      "Fetch a book from one of my shelves. I used to instruct in my younger days; "
      "teaching a princess and teaching an adventurer such as yourself should not be "
      "too different. Turn to page forty-five and begin reading. We shall test "
      "your knowledge of the material afterward, so pay attention."

classvars:

   vrName = oneCouncilor_name_rsc
   vrDesc = oneCouncilor_desc_rsc
   vrIcon = oneCouncilor_icon_rsc
   viAttributes = \
      MOB_NOFIGHT | MOB_NOMOVE | MOB_LISTEN \
         | MOB_RANDOM | MOB_RECEIVE | MOB_TEACHER
   viOccupation = MOB_ROLE_COUNCILMEMBER
   viInitialState = 201  %leaning to the princess
   viGender = GENDER_MALE

   viHomeroom = RID_HERMITHUT
   viMyRow = 4
   viMyCol = 8
   viMyAngle = ANGLE_WEST

   vrUnwanted_give = onecon_unwanted_give

properties:

   piStateBump=1

messages:

   AffectMood(why=$,what=$)
   {
      if why = MOODMOD_FACTION_CHANGE
      {
         if what = FACTION_DUKE
         {
            Send(self,@SetMood,#new_mood=piMood-2);

            return;
         }

         if what = FACTION_PRINCESS
         {
            Send(self,@SetMood,#new_mood=piMood+2);

            return;
         }
      }

      return;
   }

   Constructor()
   {
      plWantedItems = [&OneToken, &ShardImbueMajor];

      propagate;
   }

   ReqGive(what = $, item_list = $)
   {
      if item_list <> $
         AND Length(item_list) = 1
      {
         if IsClass(First(item_list),&ShardImbueMajor)
         {
            Send(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
                  #string=major_shard_delivered);
            Send(what,@GavePassiveQuestItem,#obj=First(item_list),#the_npc=self);
            return TRUE;
         }
      }
      propagate;
   }

   CheckSpecialDeliverer(who=$)
   {
      piStateBump = 1;

      if Length(Send(who,@GetSpellList)) >= NUM_SPELLS_FOR_SPECIAL
      {
         piStateBump = 2;

         return OneCon_true_ideal_rsc;
      }
      else
      {
         return OneCon_half_ideal_rsc;
      }

      return;
   }

   SetForSale()
   {
      plFor_Sale = [$,$,[SID_SHADOW_FORM]];

      return;
   }

   CanDoTeach(who=$,sid=0)
   {
      if sid = SID_SHADOW_FORM
      {
         if NOT Send(self,@CanAddSpell,#report = TRUE,#who=who,#num=sid)
         {
            return FALSE;
         }
      }

      propagate;
   }

   CanAddSpell(who=$,num=0, report = FALSE)
   {
      local lSpells, i, bOkay, oSpell;

      if who = $ OR num = 0
      {
         Debug("Called CanAddSpell with bad parameters!");

         return FALSE;
      }

      if num <> SID_SHADOW_FORM
      {
         propagate;
      }

      if Send(who,@HasSpell,#num=SID_SHADOW_FORM)
      {
         if report
         {
            Post(self,@SayToOne,#target=who,#message_rsc=onecon_already_has,
                  #parm1=Send(who,@GetLordLady));
         }

         return FALSE;
      }

      bOkay = FALSE;
      lSpells = Send(who,@GetSpellList);
      foreach i in lSpells
      {
         oSpell = Send(SYS,@FindSpellByNum,
                        #num=Send(who,@DecodeSpellNum,#compound=i));
         if Send(oSpell,@GetLevel) > 4
         {
            bOkay = TRUE;

            break;
         }
      }

      % Player doesn't have a fifth level spell, denied.
      if NOT bOkay
      {
         if report
         {
            Post(self,@SayToOne,#target=who,#message_rsc=onecon_not_ready);
         }

         return FALSE;
      }

      propagate;
   }

   SomeoneSaid(what = $,type = $,string = $)
   {
      local iResult;

      if what <> $
         AND IsClass(what,&User)
         AND StringContain(string,"train")
      {
         
         iResult = Send(what,@AttemptTrainStatPoint,#type=STAT_ID_INTELLECT);

         if iResult
         {
            Send(self,@SayToOne,#target=what,#message_rsc=teach_intellect_msg);
         }
         else
         {
            Send(self,@SayToOne,#target=what,#message_rsc=cannot_train_intellect_msg);
         }
         return;
      }
      
      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
