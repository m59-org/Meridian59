% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ThreeCouncilor is Council

constants:

   include blakston.khd
   NUM_SKILLS_FOR_SPECIAL = 8

resources:

   include threecon.lkod

   threeCouncilor_name_rsc = "Cylill"
   threeCouncilor_icon_rsc = threecon.bgf
   threeCouncilor_desc_rsc = \
      "A combat veteran of the wars with Ukgoth, he has mastered every skill "
      "of combat.  Dressed in light armor and military formal regalia, he "
      "lounges at his hut as a general during disgusting peacetime.  Cylill "
      "has had a long past serving with the Duke's forces in protecting "
      "the world from Trolls and has many war stories to tell."
      "\n\nIf you are a student with room to grow, he may help you improve "
      "your agility if you ask him to 'train' you."

   threecon_true_ideal_rsc = \
      "It is impressive to meet another who knows so much about combat skills."
   threecon_half_ideal_rsc = \
      "Given your paltry knowledge of skills, I am certainly not "
      "overly impressed."

   threecon_unwanted_give = \
      "~k%s%s tells you, \"I don't need it, chap.~n~k\""

   cannot_train_agil_msg = \
      "You seem to have full command of your dexterous potential. I cannot train you further."
   cylill_emote_msg = \
      "Cylill thwacks you suddenly!"
   cylill_emote2_msg = \
      "Cylill chases after you, whacking you with his crop!"
   teach_agil_msg = \
      "How can you ever hope to best an enemy in combat if you can't learn to dodge?!"
   teach_agil2_msg = \
      "~B~IDOOOOOOODGE!!!"

classvars:

   vrName = threeCouncilor_name_rsc
   vrDesc = threeCouncilor_desc_rsc
   vrIcon = threeCouncilor_icon_rsc
   viAttributes = \
      MOB_NOFIGHT | MOB_NOMOVE | MOB_LISTEN \
         | MOB_RANDOM | MOB_RECEIVE | MOB_TEACHER
   viOccupation = MOB_ROLE_COUNCILMEMBER
   viInitialState = 001
   viGender = GENDER_MALE

   viHomeroom = RID_F3
   viMyRow = 27
   viMyCol = 9
   viMyAngle = ANGLE_NORTH

   vrUnwanted_give = threecon_unwanted_give


properties:

   piStateBump=1

messages:

   AffectMood(why=$,what=$)
   {
      if why = MOODMOD_DAWN
      {
         Send(self,@SetMood,#new_mood=piMood+3);

         return;
      }

      if why = MOODMOD_DUSK
      {
         Send(self,@SetMood,#new_mood=piMood-3);

         return;
      }
      return;
   }

   Constructor()
   {
      plWantedItems = [&ThreeToken];

      propagate;
   }

   SetForSale()
   {
      plFor_Sale = [[Create(&ForgetPotionKnightcraft)],
                      [ SKID_DISARM,
                        SKID_CLEAVE,
                        SKID_HONESTY,
                        SKID_ACTIVE_BLOCKING_STANCE,
                        SKID_MEDIUM_ARMOR,
                        SKID_BRAVERY,
                        SKID_LIGHT_ATTACK_STANCE,
                        SKID_FORTITUDE,
                        SKID_HEAVY_ARMOR,
                        SKID_HEAVY_ATTACK_STANCE,
                        SKID_DETERMINATION,
                        SKID_ACTIVE_PARRYING_STANCE,
                        SKID_PIETY,
                        SKID_ZEALOUS_FERVOR,
                        SKID_BASH,
                        SKID_WEAPONMASTER
                      ],
                      [SKID_BATTLE_SHOUT,
                        SKID_SHIELD_BASH,
                        SKID_CHARGE_ATTACK
                      ],$];

      return;
   }

   CheckSpecialDeliverer(who=$)
   {
      piStateBump = 1;

      if Length(Send(who,@GetSkillList)) >= NUM_SKILLS_FOR_SPECIAL
      {
         piStateBump = 2;

         return threeCon_true_ideal_rsc;
      }
      else
      {
         return threeCon_half_ideal_rsc;
      }

      return;
   }

   SomeoneSaid(what = $,type = $,string = $)
   {
      local iResult;

      if what <> $
         AND IsClass(what,&User)
         AND StringContain(string,"train")
      {
         
         iResult = Send(what,@AttemptTrainStatPoint,#type=STAT_ID_AGILITY);

         if iResult
         {
            Send(what,@MsgSendUser,#message_rsc=cylill_emote_msg);
            Send(self,@SayToOne,#target=what,#message_rsc=teach_agil_msg);
            Send(what,@MsgSendUser,#message_rsc=cylill_emote2_msg);
            Send(self,@SayToOne,#target=what,#message_rsc=teach_agil2_msg);
         }
         else
         {
            Send(self,@SayToOne,#target=what,#message_rsc=cannot_train_agil_msg);
         }
         return;
      }
      
      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
