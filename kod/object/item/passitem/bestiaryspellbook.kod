% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BestiarySpellBook is PassiveItem

constants:

   include blakston.khd

resources:

   bestiary_book_name_rsc = "voluminous tome"
   bestiary_book_icon_rsc = book1.bgf
   bestiary_book_desc_rsc = \
      "This heavy book contains academic instructions on the topics of "
      "zoology, biology, and magics pertaining to organisms. "
      "\n\nFrom the chapter headings, it is apparent that this tome will teach a level %i bestiary "
      "ability called %s. %s"

   tome_generic_error_msg = \
      "This book is incomprehensible!"
   success_tome = \
      "You read through the passages, studying the information within. "
      "It takes a great deal of thought, but you start to understand the workings of "
      "living beings a little bit more deeply."
   tome_already_know_msg = \
      "You already have this knowledge."
   tome_no_more_levels_msg = \
      "You've learned all you can. These concepts are beyond your grasp."
   tome_no_previous_levels_msg = \
      "You don't know enough about the previous levels of bestiary to understand this tome."
   tome_need_more_percents = \
      "You need %i more percentage points in the previous level of bestiary before you can understand this tome."

classvars:

   vrIcon = bestiary_book_icon_rsc
   vrDesc = bestiary_book_desc_rsc

   viUse_type = ITEM_SINGLE_USE
   viUse_amount = 1
   viBulk = 1
   viWeight = 1

properties:

   piBestiarySpellID = SID_CREATURE_SPEAK
   vrName = bestiary_book_name_rsc
   viInventory_group = 3

messages:

   Constructed()
   {
      local lSkillsOne, lSkillsTwo,
            lSkillsThree, lSkillsFour, lSkillsFive, lSkillsSix,
            iRandomSkill, iRandomLevel;

      lSkillsOne = [SID_MARK_OF_AGGRESSION,
                    SID_STUDY_CREATURE_SPELLS,
                    SID_RELEASE_MINION,
                    SID_CHARM_INSECT,
                    SID_CREATURE_SPEAK];

      lSkillsTwo = [SID_CHARM_PLANT,
                    SID_STUDY_CREATURE_RESISTS,
                    SID_SOOTHING_PRESENCE,
                    SID_TOWERING_PRESENCE];
      
      lSkillsThree = [SID_MARK_OF_DEFENSE,
                      SID_CREATURE_BINDING,
                      SID_WATER_BREATHING];

      lSkillsFour = [SID_CONVOKE,
                     SID_CHARM_ANIMAL,
                     SID_GIFT_OF_VITALITY,
                     SID_GIFT_OF_SPIRIT];

      lSkillsFive = [SID_CHARM_UNDEAD,
                     SID_CHARM_HUMANOID,
                     SID_MARK_OF_MAGIC];

      lSkillsSix = [SID_CHARM_ELEMENTAL];

      iRandomLevel = 1;
      if Random(1,2) = 2
      {
         iRandomLevel = 2;
         if Random(1,2) = 2
         {
            iRandomLevel = 3;
            if Random(1,2) = 2
            {
               iRandomLevel = 4;
               if Random(1,2) = 2
               {
                  iRandomLevel = 5;
                  if Random(1,2) = 2
                  {
                     iRandomLevel = 6;
                  }
               }
            }
         }
      }
      
      switch(iRandomLevel)
      {
         case 1:
            piBestiarySpellID = Nth(lSkillsOne,Random(1,Length(lSkillsOne)));
            break;
         case 2:
            piBestiarySpellID = Nth(lSkillsTwo,Random(1,Length(lSkillsTwo)));
            break;
         case 3:
            piBestiarySpellID = Nth(lSkillsThree,Random(1,Length(lSkillsThree)));
            break;
         case 4:
            piBestiarySpellID = Nth(lSkillsFour,Random(1,Length(lSkillsFour)));
            break;
         case 5:
            piBestiarySpellID = Nth(lSkillsFive,Random(1,Length(lSkillsFive)));
            break;
         case 6:
            piBestiarySpellID = Nth(lSkillsSix,Random(1,Length(lSkillsSix)));
            break;
      }
      
      propagate;
   }

   ReqNewApply(what=$,apply_on=$)
   {
      local oSpell, iLearnAttempt;

      if what <> apply_on
      {
         return FALSE;
      }

      oSpell = Send(SYS,@FindSpellByNum,#num=piBestiarySpellID);
      if oSpell = $
      {
         return FALSE;
      }

      if Send(apply_on,@HasSpell,#num=piBestiarySpellID)
      {
         Send(apply_on,@MsgSendUser,#message_rsc=tome_already_know_msg);
         return FALSE;
      }

      iLearnAttempt = Send(apply_on,@PlayerCanLearn,#spell_num=piBestiarySpellID);

      if iLearnAttempt mod 10 = PLAYER_LEARN_BASE
      {
         Send(what,@MsgSendUser,#message_rsc=tome_need_more_percents,#parm1=iLearnAttempt/10);
         return FALSE;
      }

      switch(iLearnAttempt)
      {
         case PLAYER_LEARN_SUCCESS:
            Send(what,@MsgSendUser,#message_rsc=success_tome);
            return TRUE;
         case PLAYER_LEARN_ALREADY:
            Send(what,@MsgSendUser,#message_rsc=tome_already_know_msg);
            return FALSE;
         case PLAYER_LEARN_IMPOSSIBLE:
            Send(what,@MsgSendUser,#message_rsc=tome_no_more_levels_msg);
            return FALSE;
         case PLAYER_LEARN_NOBASE:
            Send(what,@MsgSendUser,#message_rsc=tome_no_previous_levels_msg);
            return FALSE;
      }

      Send(what,@MsgSendUser,#message_rsc=tome_generic_error_msg);

      return FALSE;
   }

   NewApplied(what = $,apply_on = $)  
   {
      Send(apply_on,@AddSpell,#num=piBestiarySpellID);
%      Post(self,@Delete);
      return;
   }

   ShowDesc()
   {
      local oSpell;
      
      oSpell=Send(SYS,@FindSpellByNum,#num=piBestiarySpellID);
      AddPacket(4, bestiary_book_desc_rsc, 4,Send(oSpell,@GetLevel), 4,Send(oSpell,@GetName), 4,Send(oSpell,@GetDesc));

      return;
   }

   NewOwner(what=$)
   {
      local oSpell;
      
      if what = $
      {
         propagate;
      }
      else
      {
         if IsClass(what,&Room)
         {
            vrName = bestiary_book_name_rsc;
            Post(what,@SomethingChanged,#what=self);
            propagate;
         }
      }

      oSpell=Send(SYS,@FindSpellByNum,#num=piBestiarySpellID);
      if oSpell <> $
         AND Send(oSpell,@GetName) <> $
      {
         vrName = Send(oSpell,@GetName);
         Post(what,@SomethingChanged,#what=self);
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
