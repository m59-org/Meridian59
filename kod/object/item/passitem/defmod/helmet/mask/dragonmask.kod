% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DragonMask is FaceMask

constants:

   include blakston.khd

resources:

   DragonMask_name_rsc = "dragon mask"
   DragonMask_icon_male_rsc = dragonbornmask.bgf
   DragonMask_icon_female_rsc = dragonbornmaskf.bgf
   DragonMask_desc_rsc = \
      "You are a draconian."

   DragonMask_wear_rsc = "Your neck extends, your nose becomes a snout, and horns emerge from your head."

classvars:

   vrName = DragonMask_name_rsc
   vrDesc = DragonMask_desc_rsc
   vrIcon = DragonMask_icon_male_rsc

   vrMaleIcon = DragonMask_icon_male_rsc
   vrFemaleIcon = DragonMask_icon_female_rsc

properties:

   % Resources that save the users face when the mask is on.
   prSaveEyes = $
   prSaveNose = $
   prSaveMouth = $

messages:

   ReqUseSomething(what = $)
   {
      if IsClass(what,&DefenseModifier)
         AND Send(what,@GetItemUseType) & ITEM_USE_HEAD
      {
         if Send(poOwner,@TryUnuseItem,#what=self)
         {
            propagate;
         }
         else
         {
            return FALSE;
         }
      }

      propagate;
   }
   
   ReqUse()
   {
      local i;
      
      if IsClass(poOwner,&Player)
      {
         foreach i in Send(poOwner,@GetPlayerUsing)
         {
            if IsClass(i,&DefenseModifier)
               AND Send(i,@GetItemUseType) & ITEM_USE_HEAD
            {
               Send(poOwner,@TryUnuseItem,#what=i);
            }
         }
      }

      propagate;
   }

   NewUsed()
   {
      % Get face info, put it on the mask.
      prSaveEyes = Send(poOwner,@GetEyesRsc);
      prSaveNose = Send(poOwner,@GetNoseRsc);
      prSaveMouth = Send(poOwner,@GetMouthRsc);

      % Make all the face overlays invis, and remove hair.
      Send(poOwner,@SetEyes,#eyes=admin_icon_blank);
      Send(poOwner,@SetNose,#nose=admin_icon_blank);
      Send(poOwner,@SetMouth,#mouth=admin_icon_blank);      
      Send(poOwner,@RemoveHair,#what=self);

      if Send(poOwner,@GetGender) = GENDER_FEMALE
      {
         Send(poOwner,@SetHead,#head=vrFemaleIcon);
         Send(poOwner,@MsgSendUser,#message_rsc=DragonMask_wear_rsc);
      }

      if Send(poOwner,@GetGender) = GENDER_MALE
      {
         Send(poOwner,@SetHead,#head=vrMaleIcon);
         Send(poOwner,@MsgSendUser,#message_rsc=DragonMask_wear_rsc);
      }

      propagate;
   }

   NewUnused()
   {
      % Put the face back on.
      Send(poOwner,@SetEyes,#eyes=prSaveEyes);
      Send(poOwner,@SetNose,#nose=prSaveNose);
      Send(poOwner,@SetMouth,#mouth=prSaveMouth);
      Send(poOwner,@ResetHair,#what=self);

      % Clear out mask resources, so we don't get ladies with man faces.
      prSaveEyes = $;
      prSaveNose = $;
      prSaveMouth = $;

      if Send(poOwner,@GetGender) = GENDER_FEMALE
      {
         Send(poOwner,@SetHead,#head=charinfo_head_kx_icon);
      }

      if Send(poOwner,@GetGender) = GENDER_MALE
      {
         Send(poOwner,@SetHead,#head=charinfo_head_ax_icon);
      }

      propagate;
   }

   GetOverlay(animation = $)
   {
      % Returning $ causes errors, and we don't want the mask wearing a mask.
      return admin_icon_blank;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


