% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
NeruditeArmor is Armor

constants:

   include blakston.khd

resources:

   include neruarmr.lkod

   neruditearmor_name_rsc = "nerudite armor"
   neruditearmor_icon_rsc = nerudamr.bgf
   neruditearmor_desc_rsc = \
      "Forged from mighty nerudite, this plate is lighter and more durable than "
      "normal plate. This chest piece exemplifies the remarkable skill of "
      "Ixla cha'Totlak. Designed as a special defense against the magics of "
      "avar shamans, this armor uses the dampening properties of nerudite to "
      "greatly resist all forms of magic. However, nerudite's inherent "
      "brittleness will cause small shards to break off and damage the wearer "
      "if hit by weapon attacks. When completely broken, it may even explode."

   neruditearmor_male_icon_rsc = btm.bgf
   neruditearmor_female_icon_rsc = btn.bgf

   neruarmor_shatters = "The nerudite armor shatters into a flurry of splinters!"

classvars:

   vrName = neruditearmor_name_rsc
   vrIcon = neruditearmor_icon_rsc
   vrDesc = neruditearmor_desc_rsc

   viHits_init_min = 850
   viHits_init_max = 900

   viValue_average = 3000
   viWeight = 500
   viBulk = 500

   viSpell_modifier = -20

   vrIcon_male = neruditearmor_male_icon_rsc
   vrIcon_female = neruditearmor_female_icon_rsc

   viDefense_base = -125
   viDamage_base = 4
   
   viHexResistance = 33

properties:

messages:

   ShowShirt()
   {
      return TRUE;
   }

   GetResistanceModifiers()
   {
      if Send(SETTINGS_OBJECT,@GetDirectArmorEnabled)
      {
         return [ [-ATCK_SPELL_FIRE,15],
                  [-ATCK_SPELL_SHOCK,15],
                  [-ATCK_SPELL_COLD,15],
                  [-ATCK_SPELL_ACID,15],
                  [-ATCK_SPELL_QUAKE,-15]
                ];
      }
      
      return [ [ATCK_WEAP_ALL,-5],
               [-ATCK_SPELL_ALL,40]
               ];
   }

   GetHeatDamage()
   {
      return 3;
   }

   DefendingHit(who = $,what = $)
   {
      if plEnchantmentMods <> $
         AND NOT Send(SETTINGS_OBJECT,@GetLoseDurabilityMagicMods)
      {
         return;
      }

      if random(1,100) < 75
      {
         piHits = piHits - 1;
      }
      
      if piHits <= 0
      {
         send(self,@ShatterToShards);
      }

      return;
   }

   DoShatter()
   {
      send(self,@ShatterToShards);

      propagate;
   }

   ShatterToShards()
   {
      if poOwner <> $ and isClass(poOwner,&User)
      {
         send(poOwner,@TryUnuseItem,#what=self);
         send(poOwner,@MsgSendUser,#message_rsc=neruarmor_shatters);

         if send(poOwner,@AssessDamage,#what=self,#damage=random(15,30),
                 #report=FALSE,#atype=ATCK_WEAP_NERUDITE) = $
         {
            send(send(poOwner,@GetOwner),@SomethingKilled,#what=self,#victim=poOwner);
            send(poOwner,@Killed,#what=self);
         }

         send(poOwner,@SomethingChanged,#what=self);
      }

      return;
   }

   GetSuffixes()
   {
      % Nerudite armor has trouble rolling weapon resistances.
      return [[&FireResistanceMod,100],
              [&ColdResistanceMod,100],
              [&ShockResistanceMod,100],
              [&AcidResistanceMod,100],
              [&PierceResistanceMod,25],
              [&SlashResistanceMod,25],
              [&BludgeonResistanceMod,25],
              [&ThrustResistanceMod,25]];
   }

   GetHexResistance()
   {
      return viHexResistance;
   }

   GetKnockbackResistance()
   {
      return 40;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

