% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ShardImbueMinor is Shard

constants:

   include blakston.khd

resources:

   basic_shard_name_rsc = "minor shard"
   basic_shard_name_plural = "minor shards"
   basic_shard_icon_rsc = purplegemshard.bgf
   basic_shard_desc = \
      "This mysterious shard of some unknown crystal "
      "will imbue a piece of mundane equipment with "
      "one or two magical modifications. "

   basic_shard_req = \
      "This shard can only add modifications to a non-magic item."
   basic_shard_applied = \
      "The %s takes on a light aura of magic."

classvars:

   vrName = basic_shard_name_rsc
   vrIcon = basic_shard_icon_rsc
   vrDesc = basic_shard_desc

   vrName_plural = basic_shard_name_plural

   vrApplied = basic_shard_applied

properties:

messages:

   ReqNewApply(what = $,apply_on = $)
   {
      local i;
      
      if what = $
         OR NOT IsClass(what,&Player)
      {
         return;
      }

      foreach i in Send(what,@GetPlayerUsing)
      {
         if i = apply_on
         {
            Send(what,@MsgSendUser,#message_rsc=vrMust_unequip);
            return FALSE;
         }
      }

      if apply_on <> $
         AND (IsClass(apply_on,&Weapon)
            OR IsClass(apply_on,&DefenseModifier)
            OR IsClass(apply_on,&Ring)
            OR IsClass(apply_on,&Gauntlet)
            OR IsClass(apply_on,&NecromancerAmulet)
            OR IsClass(apply_on,&Necklace)
            OR IsClass(apply_on,&Instrument)
            OR IsClass(apply_on,&JewelofFroz)
            OR IsClass(apply_on,&AdventurerCrest)
            OR IsClass(apply_on,&ScryingCrystal))
      {
         % This shard only applies to non-enchanted gear
         if Send(apply_on,@GetMagicModsNumber) = 0
         {
            return TRUE;
         }
         else
         {
            Send(what,@MsgSendUser,#message_rsc=basic_shard_req);
         }
      }

      return FALSE;
   }

   NewApplied(what = $,apply_on = $)
   {
      if Send(self,@GenerateMod,#oItem=apply_on,#who=what) = 1
      {
         Send(self,@SubtractNumber,#number=1);
      }
      if Random(1,2) = 1
      {
         Send(self,@GenerateMod,#oItem=apply_on,#who=what);
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
