% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ShardReImbueMinor is Shard

constants:

   include blakston.khd

resources:

   reimbue_minor_shard_name_rsc = "shard of disturbance"
   reimbue_minor_shard_name_plural = "shards of disturbance"
   reimbue_minor_shard_icon_rsc = greygemshard.bgf
   reimbue_minor_shard_desc = \
      "This mysterious shard of some unknown crystal "
      "will disturb the modifications on a weak magical "
      "item, changing their nature."

   reimbue_minor_shard_req = \
      "This shard can only change the modifications on a weak magical item."
   reimbue_minor_shard_applied = \
      "The aura around the %s wavers and changes."

classvars:

   vrName = reimbue_minor_shard_name_rsc
   vrIcon = reimbue_minor_shard_icon_rsc
   vrDesc = reimbue_minor_shard_desc

   vrName_plural = reimbue_minor_shard_name_plural

   vrApplied = reimbue_minor_shard_applied
   
   viShardColor = 68
   
   viRarity = 5

properties:

messages:

   ReqNewApply(what = $,apply_on = $)
   {
      local i;
      
      if what = $
         OR NOT IsClass(what,&Player)
      {
         return;
      }

      foreach i in Send(what,@GetPlayerUsing)
      {
         if i = apply_on
         {
            Send(what,@MsgSendUser,#message_rsc=vrMust_unequip);
            return FALSE;
         }
      }

      if Send(apply_on,@GetLadderID) <> Send(self,@GetLadderID)
         OR Send(apply_on,@GetLadderID) <> Send(what,@GetLadderID)
      {
         return FALSE;
      }
      
      if Send(apply_on,@GetForged)
      {
         Send(what,@MsgSendUser,#message_rsc=cannot_use_on_forged);
         return FALSE;
      }

      if apply_on <> $
         AND (IsClass(apply_on,&Weapon)
            OR IsClass(apply_on,&DefenseModifier)
            OR IsClass(apply_on,&Ring)
            OR IsClass(apply_on,&Gauntlet)
            OR IsClass(apply_on,&NecromancerAmulet)
            OR IsClass(apply_on,&Necklace)
            OR IsClass(apply_on,&Instrument)
            OR IsClass(apply_on,&JewelofFroz)
            OR IsClass(apply_on,&AdventurerCrest)
            OR IsClass(apply_on,&ScryingCrystal))
      {
         % This shard only applies to weakly magical gear
         if Send(apply_on,@GetMagicModsNumber) >= 1
            AND Send(apply_on,@GetMagicModsNumber) <= 2
         {
            return TRUE;
         }
         else
         {
            Send(what,@MsgSendUser,#message_rsc=reimbue_minor_shard_req);
         }
      }

      return FALSE;
   }

   NewApplied(what = $,apply_on = $)
   {
      Send(apply_on,@ClearMagicMods);
      if Send(self,@GenerateMod,#oItem=apply_on,#who=what) = 1
      {
         Send(self,@SubtractNumber,#number=1);
      }
      if Random(1,2) = 1
      {
         Send(self,@GenerateMod,#oItem=apply_on,#who=what);
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
